accumulate eIris/common/stdpp.
accumulate eIris/common/parser.
accumulate eIris/common/tokenize.
accumulate eIris/proofmode/elpi/iris_ltac.
accumulate eIris/proofmode/elpi/reduction.

shorten coq.ltac.{ open, thenl, all }.


% Manage identifiers as coq terms
pred ident->term i:ident, o:string, o:term.
ident->term (iNamed S) S T :- !,
  string->stringterm S ST,
  T = {{ INamed lp:ST }}.
ident->term (iAnon N) "anon" T :- !,
  T = {{ IAnon lp:N }}.


pred ident-for-pat i:intro_pat, i:term, o:ident, o:term.
ident-for-pat (iList [[IP]]) N ID N' :-
  ident-for-pat IP N ID N'.
ident-for-pat (iIdent ID) N ID N.
ident-for-pat _ NT (iAnon NT) NS :-
  increase-ctx-count NT NS.


pred ident-for-pat.default i:intro_pat, i:ident, i:term, o:ident, o:term.
ident-for-pat.default (iList [[IP]]) DID N ID N' :-
  ident-for-pat.default IP DID N ID N'.
ident-for-pat.default (iIdent ID) _ N ID N.
ident-for-pat.default _ (iAnon NT) N (iAnon NT) N.
ident-for-pat.default _ _ NT (iAnon NT) NS :-
  increase-ctx-count NT NS.


pred find-hyp i:ident, i:term, o:term.
find-hyp ID {{ envs_entails (Envs lp:P _ _) _ }} H :-
  ident->term ID _ IDT,
  find-hyp.aux IDT P H.
find-hyp ID {{ envs_entails (Envs _ lp:S _) _ }} H :-
  ident->term ID _ IDT,
  find-hyp.aux IDT S H.
pred find-hyp.aux i:term, i:term, o:term.
find-hyp.aux IDT {{ Esnoc _ lp:IDT lp:H }} H :- !.
find-hyp.aux IDT {{ Esnoc lp:E _ _ }} H :- !,
  find-hyp.aux IDT E H.


% Start an Iris proof
pred do-iStartProof i:hole, o:ihole.
do-iStartProof (hole {{ let _ := _ in _ }} _) _ :- !,
  coq.error "iStartProof: goal is a `let`, use `simpl`, `intros x`, `iIntros (x)`, or `iIntros ""%x""".
do-iStartProof (hole {{ @envs_entails lp:PROP (@Envs lp:PROPE lp:CI lp:CS lp:N) lp:P }} Proof) (ihole N (hole {{ @envs_entails lp:PROP (@Envs lp:PROPE lp:CI lp:CS lp:N) lp:P }} Proof)) :- !.
do-iStartProof (hole Type Proof) (ihole N (hole NType NProof)) :- 
  std.assert-ok! (coq.elaborate-skeleton {{ as_emp_valid_2 lp:Type _ (tac_start _ _) }} Type Proof) "iStartProof elaborate failed",
  Proof = {{ as_emp_valid_2 _ _ (tac_start _ lp:NProof) }},
  coq.typecheck NProof NType ok,
  NType = {{ envs_entails (Envs _ _ lp:N) _}}.


pred do-iLeft i:hole, o:hole.
do-iLeft (hole Type Proof) (hole LeftType LeftProof) :-
  @no-tc! => coq.elaborate-skeleton {{ tac_or_l _ _ _ _ _ _ }} Type Proof ok,
  Proof = {{ tac_or_l _ _ _ _ lp:FromOr lp:LeftProof }},
  coq.ltac.collect-goals FromOr [G1] _,
  open tc_solve G1 [],
  coq.typecheck LeftProof LeftType ok.


pred do-iRight i:hole, o:hole.
do-iRight (hole Type Proof) (hole RightType RightProof) :-
  @no-tc! => coq.elaborate-skeleton {{ tac_or_r _ _ _ _ _ _ }} Type Proof ok,
  Proof = {{ tac_or_r _ _ _ _ lp:FromOr lp:RightProof }},
  coq.ltac.collect-goals FromOr [G1] _,
  open tc_solve G1 [],
  coq.typecheck RightProof RightType ok.


pred do-iSplit i:bool, i:list ident, i:hole, o:hole, o:hole.
do-iSplit IsLeft Hyps (hole Type Proof) (hole LType LProof) (hole RType RProof) :-
  if (IsLeft = tt) (D = {{ Left }}) (D = {{ Right }}),
  std.map Hyps (x\r\ ident->term x _ r) HypsT,
  list->listterm HypsT ListT,
  coq.typecheck ListT {{ list _ }} ok,
  @no-tc! => coq.elaborate-skeleton {{ tac_sep_split _ lp:D lp:ListT _ _ _ _ _ }} Type Proof ok,
  Proof = {{ tac_sep_split _ _ _ _ _ _ lp:FromSep lp:ConjProof }},
  coq.ltac.collect-goals FromSep [G1] _,
  open tc_solve G1 [],
  coq.typecheck ConjProof RedType ok,
  pm-reduce RedType ConjType,
  coq.elaborate-skeleton {{ conj _ _ }} ConjType ConjProof ok,
  ConjProof = {{ conj lp:LProof lp:RProof }},
  coq.typecheck LProof LType' ok,
  pm-reduce LType' LType,
  coq.typecheck RProof RType' ok,
  pm-reduce RType' RType.
 

pred do-iAndSplit i:hole, o:hole, o:hole.
do-iAndSplit (hole Type Proof) (hole LType LProof) (hole RType RProof) :-
  @no-tc! => coq.elaborate-skeleton {{ tac_and_split _ _ _ _ _ _ _ }} Type Proof ok,
  Proof = {{ tac_and_split _ _ _ _ lp:FromAnd lp:LProof lp:RProof }},
  coq.ltac.collect-goals FromAnd [G1] _,
  open tc_solve G1 [],
  coq.typecheck LProof LType ok,
  coq.typecheck RProof RType ok.


pred do-iEvalIn i:ident, i:hole, o:ihole, o:hole.
do-iEvalIn ID (hole Type Proof) IH (hole EvalType EvalProof) :-
  ident->term ID _ T,
  pm-reduce Type Type',
  @no-tc! => coq.elaborate-skeleton {{ tac_eval_in _ lp:T _ _ _ _ _ _ _ }} Type' Proof ok,
  Proof = {{ tac_eval_in _ _ _ _ _ _ lp:Refl lp:StartProof lp:EvalProof }},
  coq.ltac.collect-goals Refl [G1] _,
  open pm_reflexivity G1 [],
  coq.typecheck StartProof StartType' ok,
  pm-reduce StartType' StartType,
  do-iStartProof (hole StartType StartProof) IH,
  coq.typecheck EvalProof EvalType' ok,
  pm-reduce EvalType' EvalType.

% Introduce a mod
pred do-iModIntro i:hole, o:hole.
do-iModIntro (hole Type Proof) (hole ModType ModProof) :-
  @no-tc! => coq.elaborate-skeleton {{ tac_modal_intro _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ }} Type Proof ok,
  Proof = {{ tac_modal_intro _ _ _ _ _ _ _ _ _ _ _ lp:TC1 lp:TC2 lp:TC3 lp:PM4 lp:SC5 lp:ModProof }},
  coq.ltac.collect-goals TC1 [G1] _,
  open tc_solve G1 [],
  coq.ltac.collect-goals TC2 [G2] _,
  open tc_solve G2 [],
  coq.ltac.collect-goals TC3 [G3] _,
  open tc_solve G3 [],
  coq.ltac.collect-goals PM4 [G4] _,
  open pm_reduce G4 [G4'],
  open tc_solve G4' [],
  coq.ltac.collect-goals SC5 [G5] _,
  open (coq.ltac.call "iSolveSideCondition" []) G5 [],
  coq.typecheck ModProof ModType ok.


pred do-intros-forall i:hole, i:(hole -> prop).
do-intros-forall (hole (prod N T F) Proof) C :- !,
  if-debug (coq.say "introducing" N),
  coq.elaborate-skeleton (fun N T _) (prod N T F) Proof ok,
  Proof = (fun _ _ IntroFProof),
  @pi-decl N T x\
    do-intros-forall (hole (F x) (IntroFProof x)) C.
do-intros-forall H F :- F H.


pred do-intro i:string, i:hole, i:(hole -> prop).
do-intro ID (hole Type Proof) C :-
  coq.ltac.collect-goals Proof [G] _,
  open (g\gl\ std.assert! (coq.ltac.id-free? ID g) "eiIntro: name already taken") G _,
  coq.id->name ID N,
  coq.elaborate-skeleton (fun N _ _) Type Proof ok,
  Proof = (fun _ T IntroFProof),
  @pi-decl N T x\ 
    coq.typecheck (IntroFProof x) (F x) ok,
    C (hole (F x) (IntroFProof x)).


pred do-intro-anon i:hole, i:(hole -> prop).
do-intro-anon  (hole (prod N' Ty _ as Type) Proof) C :-
  coq.name->id N' S,
  coq.ltac.fresh-id S Ty ID,
  coq.id->name ID N,
  coq.elaborate-skeleton (fun N _ _) Type Proof ok,
  Proof = (fun _ T IntroFProof),
  @pi-decl N T x\  
    coq.typecheck (IntroFProof x) (F x) ok,
    C (hole (F x) (IntroFProof x)).


pred do-intro-anon-external-hint i:string, i:hole, i:(hole -> prop).
do-intro-anon-external-hint S (hole (prod _ Ty _ as Type) Proof) C :-
  coq.ltac.fresh-id S Ty ID,
  coq.id->name ID N,
  coq.elaborate-skeleton (fun N _ _) Type Proof ok,
  Proof = (fun _ T IntroFProof),
  @pi-decl N T x\  
    coq.typecheck (IntroFProof x) (F x) ok,
    C (hole (F x) (IntroFProof x)).


pred do-iIntro-ident i:ident, i:ihole, o:ihole.
do-iIntro-ident ID (ihole N (hole Type Proof)) (ihole N (hole IType IProof)) :-
  ident->term ID X T,
  coq.elaborate-skeleton {{ tac_impl_intro _ lp:T _ _ _ _ _ _ _ }} Type Proof ok, !,
  Proof = {{ tac_impl_intro _ _ _ _ _ _ _ _ lp:IProof }},
  coq.typecheck IProof IType' ok,
  pm-reduce IType' IType,
  if (IType = {{ False }}) (coq.error "eiIntro: " X " not fresh") (true).
do-iIntro-ident ID (ihole N (hole Type Proof)) (ihole N (hole IType IProof)) :-
  ident->term ID X T,
  coq.elaborate-skeleton {{ tac_wand_intro _ lp:T _ _ _ _ _ }} Type Proof ok, !,
  Proof = {{ tac_wand_intro _ _ _ _ _ _ lp:IProof }}, 
  coq.typecheck IProof IType' ok,
  pm-reduce IType' IType,
  if (IType = {{ False }}) (coq.error "eiIntro: " X " not fresh") (true).
do-iIntro-ident ID _ _ :-
  ident->term ID X _,
  coq.error "eiIntro: " X " could not introduce".


pred do-iExFalso i:hole, o:hole.
do-iExFalso (hole Type Proof) (hole FalseType FalseProof) :-
  coq.elaborate-skeleton {{ tac_ex_falso _ _ _ }} Type Proof ok,
  Proof = {{ tac_ex_falso _ _ lp:FalseProof }},
  coq.typecheck FalseProof FalseType ok.


pred do-iClear i:ident, i:hole, o:hole.
do-iClear ID (hole Type Proof) (hole ClearType ClearProof) :-
  ident->term ID IDS IDT,

  @no-tc! => coq.elaborate-skeleton {{ tac_clear _ lp:IDT _ _ _ _ _ _ }} Type Proof ok,
  Proof = {{ tac_clear _ _ _ _ _ lp:Lookup lp:TC lp:ClearProof }},

  coq.ltac.collect-goals Lookup [G1] _,
  (open pm_reflexivity G1 []; coq.error "iClear:" IDS "not found"),
  coq.ltac.collect-goals TC [G2] _,
  (
    thenl [
      open pm_reduce,
      open tc_solve
    ] G2 []; 
    coq.error "iClear:" IDS "not affine and the goal not absorbing"
  ),

  coq.typecheck ClearProof RedType ok,
  pm-reduce RedType ClearType. 
  % I am skipping the simpl here, and hope it doesn't matter as much.


pred do-iPure i:ident, i:option string, i:hole, i:(hole -> prop).
do-iPure ID PID (hole Type Proof) C :-
  ident->term ID IDS IDT,

  @no-tc! => coq.elaborate-skeleton {{ tac_pure _ lp:IDT _ _ _ _ _ _ _ _ }} Type Proof ok,
  Proof = {{ tac_pure _ _ _ _ _ _ lp:Lookup lp:IntoPure lp:Affine lp:IntroProof }},

  coq.ltac.collect-goals Lookup [G1] _,
  (open pm_reflexivity G1 []; coq.error "iPure:" IDS "not found"),
  coq.ltac.collect-goals IntoPure [G2] _,
  (open tc_solve G2 []; coq.error "iPure:" IDS "not pure"),
  coq.ltac.collect-goals Affine [G3] _,
  (
    thenl [
      open pm_reduce,
      open tc_solve
    ] G3 []; 
    coq.error "iPure:" IDS "not affine and the goal not absorbing"
  ),

  coq.typecheck IntroProof IntroType ok,
  pm-reduce IntroType IntroType',
  ((
    PID = some SID, 
    do-intro SID (hole IntroType' IntroProof) C
  );
  (
    PID = none,
    do-intro-anon (hole IntroType' IntroProof) C
  )).


pred do-iPoseLem i:term, i:ihole, o:ident, i:list (hole -> prop), o:ihole.
do-iPoseLem Lem (ihole N (hole Type Proof)) (iAnon N) Cs (ihole NS (hole PosedType PosedProof)) :-
  increase-ctx-count N NS,
  ident->term (iAnon N) _ NT, !,

  @no-tc! => coq.elaborate-skeleton 
                {{ tac_pose_proof _ lp:NT _ _ (into_emp_valid_proj _ _ _ lp:Lem) _}} 
                Type Proof D,
  if (D = ok) (true) (if-debug (coq.say "pose lem error" D), fail),
  Proof = {{ tac_pose_proof _ _ _ _ (into_emp_valid_proj _ _ lp:IEVProof _) lp:PosedProof }},

  coq.ltac.collect-goals IEVProof [G1] _,
  open (coq.ltac.call "iIntoEmpValid" []) G1 GL,
  forall2-flex GL Cs (sg\ c\ open (g\gl\ sigma Proof'\ sigma Type'\ 
                                  g = (goal _ _ Type' Proof' _), !,
                                  c (hole Type' Proof')
                    ) sg _),

  coq.typecheck PosedProof NormType ok,
  pm-reduce NormType PosedType.


pred do-iPoseHyp i:term, i:ihole, o:ident, o:ihole.
do-iPoseHyp Hyp (ihole N (hole Type Proof)) (iAnon N) (ihole NS (hole PosedType PosedProof)) :-
  increase-ctx-count N NS,
  ident->term (iAnon N) _ NT,
  @no-tc! => coq.elaborate-skeleton 
                {{ tac_pose_proof_hyp _ lp:Hyp lp:NT _ _}} 
                Type Proof ok,
  Proof = {{ tac_pose_proof_hyp _ _ _ _ lp:PosedProof }},

  coq.typecheck PosedProof NormType ok,
  pm-reduce NormType PosedType,
  if (PosedType = {{ False }}) (coq.error "Could not find Hyp" {coq.term->string NormType}) (true).


pred do-iSpecializeWand i:ihole, i:ident, o:ihole, o:ihole.
do-iSpecializeWand (ihole N (hole Type Proof)) ID 
  (ihole N (hole LType LProof)) (ihole N (hole RType RProof)) :-
    ident->term ID _IDS IDT,
    @no-tc! => coq.elaborate-skeleton {{ tac_specialize_assert_no_am _ lp:IDT _ false [] _ _ _ _ _ _ _ _ _}} 
                Type Proof ok,
    Proof = {{ tac_specialize_assert_no_am _ _ _ _ _ _ _ _ _ _ lp:RefP lp:TC1P lp:TC2P lp:ConjP}},
    coq.ltac.collect-goals RefP [G1] _,
    open pm_reflexivity G1 [],
    coq.ltac.collect-goals TC1P [G2] _,
    open tc_solve G2 [],
    coq.ltac.collect-goals TC2P [G3] _,
    open tc_solve G3 [],
    coq.typecheck ConjP RedType ok,
    pm-reduce RedType ConjType,
    coq.elaborate-skeleton {{ conj _ _ }} ConjType ConjP ok,
    ConjP = {{ conj lp:LProof lp:RProof }},
    coq.typecheck LProof LType' ok,
    pm-reduce LType' LType,
    coq.typecheck RProof RType' ok,
    pm-reduce RType' RType.


pred do-iApplySimpleHyp i:ihole, i:ident, o:ihole.
do-iApplySimpleHyp (ihole N (hole Type Proof)) ID (ihole N (hole ApType ApProof)) :-
  ident->term ID _IDS IDT,
  @no-tc! => coq.elaborate-skeleton {{ tac_apply _ lp:IDT _ _ _ _ _ _ _ }} Type Proof D,
  if (D = ok) (true) (if-debug (coq.say "simple apply hyp failed" D), fail),
  Proof = {{ tac_apply _ _ _ _ _ _ lp:RefProof lp:TCProof lp:ApProof }},
  coq.ltac.collect-goals RefProof [G1] _,
  open pm_reflexivity G1 [],
  coq.ltac.collect-goals TCProof [G2] _,
  open tc_solve G2 [],
  coq.typecheck ApProof RedType ok,
  pm-reduce RedType ApType.


pred do-iApplySimpleExact i:ihole, i:ident.
do-iApplySimpleExact (ihole _ (hole Type Proof)) ID :-
  ident->term ID _IDS IDT,
  @no-tc! => coq.elaborate-skeleton {{ tac_assumption _ lp:IDT _ _ _ _ _ _ }} Type Proof D,
  if (D = ok) (true) (if-debug (coq.say "simple apply exact failed" D), fail),
  Proof = {{ tac_assumption _ _ _ _ _ lp:RefProof lp:TCProof lp:RTCProof }},
  coq.ltac.collect-goals RefProof [G1] _,
  open pm_reflexivity G1 [],
  coq.ltac.collect-goals TCProof [G2] _,
  open tc_solve G2 [],
  coq.ltac.collect-goals RTCProof [G3] _,
  open (pm_reduce) G3 [G3'],
  open tc_solve G3' [].


pred do-iApplyLem i:term, i:ihole, i:list (hole -> prop), o:list ihole.
do-iApplyLem Lem IH Cs IHS :-
  if-debug (coq.say "do-iApplyLem" {coq.term->string Lem}),
  do-iPoseLem Lem IH ID Cs IHPosed, !,
  if-debug (coq.say "Posed Lemma" {ihole->string IHPosed}),
  do-iApplyLem.aux IHPosed ID IHS.
do-iApplyLem.aux IH ID [] :-
  do-iApplySimpleExact IH ID,
  if-debug (coq.say "simpleApplyExact success").
do-iApplyLem.aux IH ID [IH'] :-
  do-iApplySimpleHyp IH ID IH',
  if-debug (coq.say "simpleApplyHyp sucess" {ihole->string IH'}).
do-iApplyLem.aux IH ID [IHL | IHS] :-
  do-iSpecializeWand IH ID IHL IHR,
  if-debug (coq.say "Specialized Lemma" {ihole->string IHL} {ihole->string IHR}),
  do-iApplyLem.aux IHR ID IHS.


pred do-iApplyHyp i:string, i:ihole, o:list ihole.
do-iApplyHyp HypString IH IHS :-
  string->stringterm HypString Hyp,
  if-debug (coq.say "do-iApplyHyp" HypString ":-" {ihole->string IH}),
  do-iPoseHyp Hyp IH ID IHPosed, !,
  if-debug (coq.say "Posed Hyp" {ihole->string IHPosed}),
  do-iApplyHyp.aux IHPosed ID IHS.
do-iApplyHyp.aux IH ID [] :-
  do-iApplySimpleExact IH ID,
  if-debug (coq.say "simpleApplyExact success").
do-iApplyHyp.aux IH ID [IH'] :-
  do-iApplySimpleHyp IH ID IH',
  if-debug (coq.say "simpleApplyHyp sucess" {ihole->string IH'}).
do-iApplyHyp.aux IH ID [IHL | IHS] :-
  do-iSpecializeWand IH ID IHL IHR, !,
  if-debug (coq.say "Specialized Lemma" {ihole->string IHL} {ihole->string IHR}),
  do-iApplyHyp.aux IHR ID IHS.


pred do-iIntuitionistic i:ident, i:hole, o:hole.
do-iIntuitionistic ID (hole Type Proof) (hole IntType IntProof) :-
  ident->term ID X T,

  @no-tc! => coq.elaborate-skeleton {{ tac_impl_intro_intuitionistic _ lp:T _ _ _ _ _ _ _ }} Type Proof ok,
  Proof = {{ tac_impl_intro_intuitionistic _ _ _ _ _ _ lp:FromImpl lp:IP lp:IntProof }},

  coq.ltac.collect-goals FromImpl [GI1] _,
  open (tc_solve) GI1 [], 
  !,
  coq.ltac.collect-goals IP [GI2] _,
  (open (tc_solve) GI2 []; coq.error "eiIntuitionistic: not intuitionistic"),

  coq.typecheck IntProof RedType ok,
  pm-reduce RedType IntType,
  if (IntType = {{ False }}) (coq.error "eiIntuitionistic: " X " not fresh") (true).

do-iIntuitionistic ID (hole Type Proof) (hole IntType IntProof) :-
  ident->term ID X T,

  @no-tc! => coq.elaborate-skeleton {{ tac_wand_intro_intuitionistic _ lp:T _ _ _ _ _ _ _ _ }} Type Proof ok,
  Proof = {{ tac_wand_intro_intuitionistic _ lp:T _ _ _ _ lp:FromWand lp:IP lp:TCOR lp:IntProof }},

  coq.ltac.collect-goals FromWand [GW1] _,
  open (tc_solve) GW1 [], 
  !,
  coq.ltac.collect-goals IP [GW2] _,
  (open (tc_solve) GW2 []; coq.error "eiIntuitionistic: not intuitionistic"),
  coq.ltac.collect-goals TCOR [GW3] _,
  (open (tc_solve) GW3 []; coq.error "eiIntuitionistic: not affine and the goal not absorbing"),

  coq.typecheck IntProof RedType ok,
  pm-reduce RedType IntType,
  if (IntType = {{ False }}) (coq.error "eiIntuitionistic: " X " not fresh") (true).

do-iIntuitionistic ID _ _ :- !,
  ident->term ID X _,
  coq.error {calc ("eiIntuitionistic: " ^ X ^ " could not introduce")}.


pred do-iAndDestruct i:ident, i:ident, i:ident, i:hole, o:hole.
do-iAndDestruct HID H1ID H2ID (hole Type Proof) (hole AndType AndProof) :-
  ident->term HID ID HIDT,
  ident->term H1ID _ H1IDT,
  ident->term H2ID _ H2IDT,

  @no-tc! => coq.elaborate-skeleton {{ tac_and_destruct _ lp:HIDT _ lp:H1IDT lp:H2IDT _ _ _ _ _ _ _ }} Type Proof ok,
  Proof = {{ tac_and_destruct _ _ _ _ _ _ _ _ _ lp:Lookup lp:Into lp:AndProof }},

  coq.ltac.collect-goals Lookup [G1] _,
  (open pm_reflexivity G1 []; coq.error "iAndDestruct:" ID "not found"),
  coq.ltac.collect-goals Into [G2] _,
  (
    thenl [
      open pm_reduce,
      open tc_solve
    ] G2 []; 
    (open show-goal G2 _, coq.error "iAndDestruct: cannot destruct")
  ),

  coq.typecheck AndProof RedType ok,
  pm-reduce RedType AndType,
  if (AndType = {{ False }}) (coq.error "iAndDestruct: left or right not fresh") (true).


pred do-iAndChoiceDestruct i:ident, i:ident, i:bool, i:hole, o:hole.
do-iAndChoiceDestruct ID OID ChooseLeft (hole Type Proof) (hole AndType AndProof) :-
  ident->term ID X IDT,
  ident->term OID OX OIDT,

  if (ChooseLeft = tt) (D = {{ Left }}) (D = {{ Right }}),

  @no-tc! => std.assert-ok! (coq.elaborate-skeleton {{ tac_and_destruct_choice _ lp:IDT _ lp:D lp:OIDT _ _ _ _ _ _ _ }} Type Proof) "iAndChoiceDestruct failed",
  Proof = {{ tac_and_destruct_choice _ _ _ _ _ _ _ _ _ lp:Lookup lp:Into lp:AndProof }},

  coq.ltac.collect-goals Lookup [G1] _,
  (open pm_reflexivity G1 []; coq.error "iAndChoiceDestruct:" X "not found"),
  coq.ltac.collect-goals Into [G2] _,
  (
    thenl [
      open pm_reduce,
      open tc_solve
    ] G2 []; 
    (open show-goal G2 _, coq.error "iAndChoiceDestruct: cannot destruct")
  ),

  coq.typecheck AndProof RedType ok,
  pm-reduce RedType AndType,
  if (AndType = {{ False }}) (coq.error "iAndChoiceDestruct: " OX " not fresh") (true).


pred do-iOrDestruct i:ident, i:ident, i:ident, i:hole, o:hole, o:hole.
do-iOrDestruct HID H1ID H2ID (hole Type Proof) (hole OrLType OrLProof) (hole OrRType OrRProof) :-
  ident->term HID ID HIDT,
  ident->term H1ID _ H1IDT,
  ident->term H2ID _ H2IDT,
  
  @no-tc! => coq.elaborate-skeleton {{ tac_or_destruct _ lp:HIDT _ lp:H1IDT lp:H2IDT _ _ _ _ _ _ _ }} Type Proof ok,
  Proof = {{ tac_or_destruct _ _ _ _ _ _ _ _ _ lp:Lookup lp:Into lp:ConjProof }},
  
  coq.ltac.collect-goals Lookup [G1] _,
  (open pm_reflexivity G1 []; (open show-goal G1 _, coq.error "iOrDestruct:" ID "not found")),
  coq.ltac.collect-goals Into [G2] _,
  if-debug (coq.say "intoOr", open show-goal G2 _),
  (open tc_solve G2 []; (open show-goal G2 _, coq.say "iOrDestruct: cannot destruct")),
  
  coq.typecheck ConjProof RedType ok,
  pm-reduce RedType ConjType,
  if (ConjType = {{ False }}) (coq.error "iAndDestruct: left or right not fresh") (true),

  coq.elaborate-skeleton {{ conj _ _ }} ConjType ConjProof ok,
  ConjProof = {{ conj lp:OrLProof lp:OrRProof }},
  coq.typecheck OrLProof OrLType ok,
  coq.typecheck OrRProof OrRType ok.

% Backtrack or not on errors
macro @set-bt-error! B :- (get-option "bt-error" BT_FAIL_VAL :- !, BT_FAIL_VAL = B).
macro @bt-error! B :- get-option "bt-error" B.

pred do-iExistDestruct i:ident, i:option string, i:ident, i:hole, i:(hole -> prop).
do-iExistDestruct ID X HID (hole Type Proof) C :- !,
  ident->term ID _ IDT,
  ident->term HID _ HIDT,

  @no-tc! => coq.elaborate-skeleton {{ tac_exist_destruct_without_name _ lp:IDT _ lp:HIDT _ _ _ _ _ _ _ }} Type Proof ok,
  Proof = {{ tac_exist_destruct_without_name _ _ _ _ _ _ _ _ lp:Lookup lp:Into lp:ExistProof }},

  coq.ltac.collect-goals Lookup [G1] _,
  (open pm_reflexivity G1 []; 
    (open (goal->string G1S) G1 _, if-debug (coq.say "iExistDestruct:" ID "not found" G1S), if (@bt-error! ff) (fail) (coq.error "iExistDestruct:" ID "not found" G1S))
  ),!,
  coq.ltac.collect-goals Into [G2] _,
  (open tc_solve G2 []; 
    (open (goal->string G2S) G2 _, if-debug (coq.say "iExistDestruct: cannot destruct" G2S), if (@bt-error! ff) (fail) (coq.error "iExistDestruct: cannot destruct" G2S))
  ),!,

  coq.typecheck ExistProof RedType ok,
  pm-reduce RedType ExistType,
  if (X = some XN) 
    (do-intro XN (hole ExistType ExistProof) C)
    (
      Proof = {{ tac_exist_destruct_without_name _ _ _ _ _ _ lp:NameT _ _ _ _ }},
      NameT = (fun Name _ _),
      coq.name->id Name S,
      if (rex.match "^elpi_ctx_entry_" S) (S' = "a") (S = S'), 
      do-intro-anon-external-hint S' (hole ExistType ExistProof) C
    ).


pred do-iExact i:ident, i:hole.
do-iExact ID (hole Type Proof) :-
  ident->term ID IDS IDT,

  @no-tc! => coq.elaborate-skeleton {{ tac_assumption _ lp:IDT _ _ _ _ _ _ }} Type Proof ok,
  Proof = {{ tac_assumption _ _ _ _ _ lp:Lookup lp:FromAs lp:TC }},

  coq.ltac.collect-goals Lookup [G1] _,
  (open pm_reflexivity G1 []; coq.error "iExact:" IDS "not found"),
  coq.ltac.collect-goals FromAs [G2] _,
  (open tc_solve G2 []; coq.error "iExact:" IDS "does not match goal"),
  coq.ltac.collect-goals TC [G3] _,
  (
    thenl [
      open pm_reduce,
      open tc_solve
    ] G3 [];
    coq.error "iExact: remaining hypotheses not affine and the goal not absorbing"
  ).


pred do-iDestruct i:ident, o:intro_pat, i:ihole, i:(ihole -> prop).
:name "do-iDestruct-start"
do-iDestruct ID IP _ _ :- if-debug (coq.say "do-iDestruct: " ID IP), fail.
do-iDestruct ID (iList [[IP]]) IH C :- !, do-iDestruct ID IP IH C.
do-iDestruct ID (iIdent ID) IH C :- !, C IH.
do-iDestruct (iAnon _) iFresh IH C :- !, C IH.
do-iDestruct ID iDrop (ihole N H) C :- !, do-iClear ID H H', C (ihole N H').
do-iDestruct ID (iPure PID) (ihole N H) C :- !, do-iPure ID PID H (h\ C (ihole N h)).
do-iDestruct ID (iList [[]]) (ihole _ H) _ :-
  do-iExFalso H H',
  do-iExact ID H'.
do-iDestruct ID (iList [[iForall | IPSS]]) IH C :-
  @set-bt-error! ff => do-iDestruct ID (iList [[iPure none, iForall | IPSS]]) IH C.
do-iDestruct ID (iList [[iForall | IPSS]]) IH C :- !,
  @set-bt-error! tt => do-iDestruct ID (iList [IPSS]) IH C.
do-iDestruct ID (iAll) (ihole N (hole Type Proof)) C :-
  find-hyp ID Type (app [global GREF | _]),
  inductive-unfold GREF _ _ _ NConstr, !,
  std.map {std.iota NConstr} (x\r\ r = [iFresh]) IPSS,
  do-iDestruct ID (iList IPSS) (ihole N (hole Type Proof)) C.
do-iDestruct ID (iList IPSS) (ihole N (hole Type Proof)) C :-
  find-hyp ID Type (app [global GREF | _]),
  inductive-unfold GREF U1 _ _ NConstr, !,
  if-debug (coq.say "Destructing ind" {coq.term->string (global GREF)} NConstr),
  do-iEvalIn ID (hole Type Proof) IH H,
  do-iApplyLem (global U1) IH [] [],
  do-iDestruct ID (iList IPSS) (ihole N H) C.
do-iDestruct ID (iList [[iPure PN, IP | IPS]]) (ihole N H) C :- !,
  ident-for-pat.default IP ID N HID NS,
  do-iExistDestruct ID PN HID H (h\ do-iDestruct HID (iList [[IP | IPS]]) (ihole NS h) C).
  % This case now also handles the pure and exists case with typeclasses,
  % however, that is not neccessary as we can just backtrack in here
  % And take the next case if iExistsDestruct fails.
do-iDestruct ID (iList [[IP, iDrop]]) (ihole N H) C :- !,
  ident-for-pat.default IP ID N HID N',
  do-iAndChoiceDestruct ID HID tt H H', !,
  do-iDestruct HID IP (ihole N' H') C.
do-iDestruct ID (iList [[iDrop, IP]]) (ihole N H) C :- !,
  ident-for-pat.default IP ID N HID N',
  do-iAndChoiceDestruct ID HID ff H H', !,
  do-iDestruct HID IP (ihole N' H') C.
do-iDestruct ID (iList [[IP1, IP2 | IPSS]]) (ihole N H) C :-
  ident-for-pat.default IP1 ID N ID1 N',
  ident-for-pat IP2 N' ID2 N'',
  do-iAndDestruct ID ID1 ID2 H H', !,
  do-iDestruct ID1 IP1 (ihole N'' H') (ih\ do-iDestruct ID2 (iList [[IP2 | IPSS]]) ih C).
do-iDestruct ID (iList [IPSL, IPSR | IPSS]) (ihole N H) C :-
  ident-for-pat.default (iList [IPSL]) ID N ID1 N',
  ident-for-pat (iList [IPSR]) N' ID2 N'',
  do-iOrDestruct ID ID1 ID2 H HL HR, !,
  do-iDestruct ID1 (iList [IPSL]) (ihole N'' HL) C, !,
  do-iDestruct ID2 (iList [IPSR | IPSS]) (ihole N'' HR) C.
do-iDestruct _ IP IH C :-
  coq.say "eiDestruct: Skipping " {std.any->string IP}, !,
  C IH.

pred do-iIntros i:(list intro_pat), i:ihole, i:(ihole -> prop).
do-iIntros [IP | _] IH _ :- if-debug (coq.say "do-iIntros: " IP "in" {ihole->string IH}), fail.
do-iIntros [] IH C :- !, if-debug (coq.say "do-iIntros done" {ihole->string IH}), C IH.
do-iIntros [iCoqIntro X | IPS] (ihole N (hole _ Proof)) C :-
  coq.ltac.collect-goals Proof [G] _,
  open (coq.ltac.call-ltac1 X) G [GTac], !,
  open (g\gl\ sigma TacProof TacType\ 
          g = (goal _ _ TacType TacProof _), !,
          do-iIntros IPS (ihole N (hole TacType TacProof)) C
       ) GTac _.
do-iIntros [iSimpl | IPS] (ihole N (hole _ Proof)) C :-
  coq.ltac.collect-goals Proof [G] _,
  open pm-reduce-goal G [G'], !,
  open simpl G' [G''], !,
  open (g\gl\ sigma SimplProof SimplType\ 
          g = (goal _ _ SimplType SimplProof _), !,
          do-iIntros IPS (ihole N (hole SimplType SimplProof)) C
       ) G'' _.
do-iIntros [iDone | IPS] (ihole N (hole Type Proof)) C :-
  ((
    coq.ltac.collect-goals Proof [G] _,
    open pm-reduce-goal G [G'], !,
    open (g\gl\ sigma T P\ g = (goal _ _ T P _), if-debug (coq.say "ahhh is on" T P)) G' _, !, % weird hack, without it things don't work. Don't know why
    open done G' [],
    if-debug (coq.say "done succeeded")
    ); 
    do-iIntros IPS (ihole N (hole Type Proof)) C
  ).
do-iIntros [iFresh | IPS] (ihole N H) C :- !,
  increase-ctx-count N NS,
  do-iIntro-ident (iAnon N) (ihole NS H) IH, !, 
  do-iIntros IPS IH C.
do-iIntros [iDrop | IPS] (ihole N (hole Type Proof)) C :- !,
  (
    (coq.elaborate-skeleton {{ tac_impl_intro_drop _ _ _ _ _ _ }} Type Proof ok, Proof = {{ tac_impl_intro_drop _ _ _ _ _ lp:IntroProof }});
    (coq.elaborate-skeleton {{ tac_wand_intro_drop _ _ _ _ _ _ _ }} Type Proof ok, Proof = {{ tac_wand_intro_drop _ _ _ _ _ _ lp:IntroProof }});
    % TODO: Not sure what the forall case is.
    (!, coq.error "eiIntro: Could not introduce", fail)
  ),
  coq.typecheck IntroProof IntroType ok, !,
  do-iIntros IPS (ihole N (hole IntroType IntroProof)) C.
do-iIntros [iPure (some X) | IPS] (ihole N (hole Type Proof)) C :-
  coq.elaborate-skeleton {{ tac_forall_intro_nameless _ _ _ _ _ _ }} Type Proof ok,
  Proof = {{ tac_forall_intro_nameless _ _ _ _ _ lp:IProof }},
  coq.typecheck IProof IType ok, !,
  do-intro X (hole IType IProof) (h\ sigma IntroProof\ sigma IntroType\ sigma NormType\
    h = hole IntroType IntroProof,
    coq.reduction.lazy.bi-norm IntroType NormType, !,
    do-iIntros IPS (ihole N (hole NormType IntroProof)) C
  ).
do-iIntros [iPure (none) | IPS] (ihole N (hole Type Proof)) C :-
  coq.elaborate-skeleton {{ tac_forall_intro_nameless _ _ _ _ _ _ }} Type Proof ok,
  Proof = {{ tac_forall_intro_nameless _ _ _ _ _ lp:IProof }},
  coq.typecheck IProof IType ok, !,
  do-intro-anon (hole IType IProof) (h\ sigma IntroProof\ sigma IntroType\ sigma NormType\
    h = hole IntroType IntroProof,
    coq.reduction.lazy.bi-norm IntroType NormType, !,
    do-iIntros IPS (ihole N (hole NormType IntroProof)) C
  ).
  % coq.ltac.collect-goals IProof [G] _,
  % open (coq.ltac.call "intros_anon" []) G [G'],
  % open (g\gl\ sigma AnonRedType\ sigma AnonType\ sigma AnonProof\ 
  %           g = (goal _ _ AnonRedType AnonProof _), 
  %           coq.reduction.lazy.bi-norm AnonRedType AnonType, !,
  %           do-iIntros IPS (ihole N (hole AnonType AnonProof)) C
  %       ) G' _.
do-iIntros [iIdent ID | IPS] IH C :-
  do-iIntro-ident ID IH IH', !,
  do-iIntros IPS IH' C.
do-iIntros [iModalIntro | IPS] (ihole N H) C :- !,
  do-iModIntro H H', !,
  do-iIntros IPS (ihole N H') C.
do-iIntros [iHyp H | IPS] IH C :-
  do-iApplyHyp H IH IHS, !,
  std.forall IHS (ih\ do-iIntros IPS ih C).
do-iIntros [iIntuitionistic iFresh | IPS] (ihole N H) C :- !,
  increase-ctx-count N NS,
  do-iIntuitionistic (iAnon N) H H', !,
  do-iIntros IPS (ihole NS H') C.
do-iIntros [iIntuitionistic (iIdent ID) | IPS] (ihole N H) C :-
  do-iIntuitionistic ID H H', !,
  do-iIntros IPS (ihole N H') C.
do-iIntros [iIntuitionistic (iList IPS) | IPSS] (ihole N H) C :-
  increase-ctx-count N NS,
  do-iIntuitionistic (iAnon N) H H', !,
  do-iDestruct (iAnon N) (iList IPS) (ihole NS H') (ih\ !, do-iIntros IPSS ih C).
do-iIntros [iList IPS | IPSS] (ihole N H) C :- !,
  increase-ctx-count N NS,
  do-iIntro-ident (iAnon N) (ihole NS H) IH, !,
  do-iDestruct (iAnon N) (iList IPS) IH (ih'\ !,
    do-iIntros IPSS ih' C
  ).
% do-iIntros IPS IH _ :-
%   coq.error "Failed to apply" IPS "in hole" {ihole->string IH}.