accumulate eIris/proofmode/elpi/proper_solver.


pred constructor->term i:indc-decl, o:term.
constructor->term (constructor _ Arity) T :- coq.arity->term Arity T.


pred find-PROP i:term, o:term.
find-PROP (prod _ _ F) O :- !,
  (pi x\ find-PROP (F x) O).
find-PROP O O :- !.


pred ne-to-prod i:term, o:term.
ne-to-prod (prod N T F) (prod N T F') :- !,
  (pi x\ ne-to-prod (F x) (F' x)).
ne-to-prod {{ lp:A -n> lp:B }} (prod _ A (x\ B')) :- !,
  ne-to-prod B B'.
ne-to-prod T T.


pred init-prod-to-bi-exist i:term, o:term.
init-prod-to-bi-exist (prod N T F) {{ bi_exist lp:Fun}} :- !,
  (pi x\ init-prod-to-bi-exist (F x) (F' x)),
  Fun = (fun N T F').
init-prod-to-bi-exist X X.


pred top-wand-to-sepand i:term, o:term.
top-wand-to-sepand {{ bi_emp_valid lp:T }} T' :- !,
  top-wand-to-sepand T T'.
top-wand-to-sepand {{ bi_exist lp:{{ fun N T F}} }} {{ bi_exist lp:{{ fun N T F' }} }} :- !,
  (pi x\ top-wand-to-sepand (F x) (F' x)).
top-wand-to-sepand {{ bi_forall lp:{{ fun N T F}} }} {{ bi_forall lp:{{ fun N T F' }} }} :- !,
  (pi x\ top-wand-to-sepand (F x) (F' x)).
top-wand-to-sepand {{ bi_wand lp:L lp:R }} {{ bi_sep lp:L lp:R' }} :- !,
  top-wand-to-sepand R R'.
top-wand-to-sepand {{ bi_sep lp:L lp:R }} {{ bi_sep lp:L lp:R' }} :- !,
  top-wand-to-sepand R R'.
top-wand-to-sepand X X :- !.


pred mk-constr-body-disj i:term, i:(term -> list indc-decl), o:int, o:(term -> list indc-decl), o:(term -> term).
mk-constr-body-disj FType Constructors NConstr BIConstructors ConstrBo :-
  if-debug (coq.sigma.print),
  if-debug ((pi x\ print-contructors (Constructors x))),
  (pi f\ std.length (Constructors f) NConstr),
  % Transform base constructor into shape of a bi prop
  (pi f\ std.map (Constructors f)
    (x\ r\ sigma TMP1 TMP2\ 
      constructor->term x TMP1, 
      init-prod-to-bi-exist TMP1 TMP2, 
      top-wand-to-sepand TMP2 r)
    (ConstrBiTerms f)),
  coq.string->name "rec" N,
  if-debug ((@pi-decl N FType f\ coq.say "------ Constructor Bi Terms pre elaboration" {std.map (ConstrBiTerms f) coq.term->string})),
  % Create constructors for later use in induction.
  (@pi-decl N FType f\ std.map2 (Constructors f) (ConstrBiTerms f)
                 (x\ y\ r\ sigma ID M Y\ x = constructor ID _,
                                       M is "eIris Failed elaborating constructor " ^ ID,
                                       (@reversible! => @primitive! => std.assert-ok! (coq.elaborate-skeleton y _ Y) M), 
                                       r = constructor ID (arity y))
                 (BIConstructors f)),
  if-debug ((@pi-decl N FType f\ coq.say "------ Constructor Bi Terms", print-contructors (BIConstructors f))),
  % connect constructors with bi_or's
  (pi f\ std.fold-right
    { std.drop-last 1 (ConstrBiTerms f) }
    { std.last (ConstrBiTerms f) }
    (x\ a\ r\ r = {{ bi_or lp:x lp:a }}) 
    (ConstrBo f)),
  if-debug (coq.say "------ Constructor body disjunction" {coq.term->string (ConstrBo {{ True }})}).


pred type-to-fun i:term, o:term.
type-to-fun (prod N T F) (fun N T FB) :- !,
  (pi x\ type-to-fun (F x) (FB x)).
type-to-fun X X :- !.


pred last-rec-to-and i:list term, i:term, o:list (pair term term), o:term.
last-rec-to-and Binders (fun N T F) Replacements (fun N T F') :- !,
  (pi x\ last-rec-to-and [x | Binders] (F x) Replacements (F' x)).
last-rec-to-and Binders {{ bi_or lp:L lp:R }} [] {{ bi_or lp:L' lp:R' }} :- !,
  last-rec-to-and Binders L _ L',
  last-rec-to-and Binders R _ R'.
last-rec-to-and Binders {{ bi_exist lp:{{ fun N T F }} }} Replacements' ResTerm :- !,
  (pi x\ sigma Replacements ReplaceTerm\
    last-rec-to-and Binders (F x) Replacements (F' x),
    (
      (
        if-debug (coq.say "searching " x " in " Replacements " for binder " N "(ctx: " {coq.term->string (F' x)} ")"),
        std.lookup Replacements x ReplaceTerm, !,
        if-debug (coq.say "found " x " replacing with " ReplaceTerm),
        std.filter Replacements (t\ sigma TMP\ fst t TMP, not (x = TMP)) Replacements',
        copy x ReplaceTerm => copy (F' x) ResTerm
      );
      (Replacements' = Replacements, ResTerm = {{ bi_exist lp:{{ fun N T F' }} }})
    )
  ).
last-rec-to-and Binders {{ bi_sep lp:L lp:R }} Replacements SepTerm :- !,
  last-rec-to-and Binders R Replacements R',
  if (R' = {{ (⌜True⌝)%I }})
    (SepTerm = L)
    (SepTerm = {{ bi_sep lp:L lp:R' }}).
last-rec-to-and BindersRev (app [FixBinder | TS]) Replacements EQTerm :-
  std.rev BindersRev [FixBinder | BS],
  {std.length BS} = {std.length TS}, !,
  last-rec-to-and.eqs BS TS EQs Replacements,
  last-rec-to-and.duplicates Replacements DupEQs,
  (
    (
      {std.append EQs DupEQs} = [EQ | EQs'],
      std.fold EQs' EQ (x\ y\ r\ r = {{ bi_sep lp:x lp:y }}) EQTerm
    ) ;
    (EQTerm = {{ (⌜True⌝)%I }})
  ).
last-rec-to-and LS (app [T | TS]) _ _ :-
  coq.error "EI.Ind: " {coq.term->string (app [T | TS])} "has to many or to few arguments (or something else went wrong), expected " {coq.term->string (app {std.rev LS})}.

pred last-rec-to-and.eqs i:list term, i:list term, o:list term, o:list (pair term term).
last-rec-to-and.eqs [] [] [] [] :- !. 
last-rec-to-and.eqs [B | BS] [T | TS] EQs [pr T B | Replacements] :-
  name T, !,
  if-debug (coq.say "replacing term" T "with" B),
  last-rec-to-and.eqs BS TS EQs Replacements.
last-rec-to-and.eqs [B | BS] [T | TS] [{{ (⌜lp:B = lp:T⌝)%I }} | EQs] Replacements :- !,
  if-debug (coq.say "equating term" T "with" B),
  last-rec-to-and.eqs BS TS EQs Replacements.

pred last-rec-to-and.duplicates i:list (pair term term), o:list term.
last-rec-to-and.duplicates [] [].
last-rec-to-and.duplicates [pr EB FB | Repls] CombinedEQs :- 
  std.findall (std.lookup Repls EB _) Lookups,
  std.map Lookups (x\r\ x = std.lookup _ _ r) FBs,
  std.map FBs (x\r\ r = {{ (⌜lp:FB = lp:x⌝)%I }}) EQs,
  last-rec-to-and.duplicates Repls EQs',
  std.append EQs EQs' CombinedEQs.


pred mk-constr-body i:list param, i:term, i:(term -> list indc-decl), o:int, o:(term -> list indc-decl), o:term, o:term.
mk-constr-body Params TypeTerm Constructors NConstr BIConstructors EBo Ty :-
  find-PROP TypeTerm PROP,
  mk-constr-body-disj TypeTerm Constructors NConstr BIConstructors ConstrBo,
  (pi b\ (type-to-fun PROP b :- !) => type-to-fun TypeTerm (FunTerm b)),
  BodyWithoutReplacements = {{ fun rec : lp:TypeTerm => lp:{{ FunTerm (ConstrBo {{ rec }}) }} }},
  last-rec-to-and [] BodyWithoutReplacements _ Bo,
  if-debug (coq.say "------ Body without params" {coq.term->string Bo}),
  replace-params-bo Params Bo PBo,
  if-debug (coq.say "------- Body" { coq.term->string PBo }),
  replace-params-ty Params {{ lp:TypeTerm -> lp:TypeTerm }} Ty, !,
  if-debug (coq.say "------- Type" { coq.term->string Ty }),
  (@reversible! => @primitive! => std.assert-ok! (coq.elaborate-skeleton PBo Ty EBo) "Type check body failed").


pred mk-proper i:list param, i:term, i:term, o:term.
mk-proper Params F Type EBo :-
  find-PROP Type PROP,
  ((pi N T F A T1 F1 A1 \ fold-map (prod N T F) A (prod N T1 F1) (some {{ (.> lp:A1)%i_signature }}) :-
        fold-map T A T1 _, !, (pi x\ fold-map (F x) A (F1 x) (some A1))) =>
    (pi A \ fold-map PROP A PROP (some {{ bi_wand }}) :- !) =>
      fold-map Type none Type (some R)),
  std.map Params (x\r\ x = (par _ _ _ r)) Ps,
  Fapp = app [F | Ps],
  replace-params-ty Params {{ IProper (□> lp:R ==> lp:R) lp:Fapp }} Proper,
  @keepunivs! => std.assert-ok! (coq.elaborate-skeleton Proper {{ Prop }} EBo) "Type check proper failed".


pred proper-proof i:term, o:term.
proper-proof Proper Proof :-
  coq.typecheck Proof Proper ok,
  do-solve-proper (hole Proper Proof).


pred mk-ne-sig i:term, i:term, o:term.
mk-ne-sig N (prod _ _ F) Sig :- !,
  (pi x\ mk-ne-sig N (F x) Sig).
mk-ne-sig N {{ _ (_ -n> lp:NEType) }} {{ (dist lp:N ==> lp:Sig)%signature }} :- !,
  mk-ne-sig N NEType Sig.
mk-ne-sig N {{ _ -n> lp:NEType }} {{ (dist lp:N ==> lp:Sig)%signature }} :- !,
  mk-ne-sig N NEType Sig.
mk-ne-sig N _ {{ dist lp:N }}.


pred mk-ne-phi-ass i:term, i:term, i:list term, o:term.
mk-ne-phi-ass Phi (prod N T F) Args (prod N T F') :- !,
  (pi x\ mk-ne-phi-ass Phi (F x) [x | Args] (F' x)).
mk-ne-phi-ass Phi NEType RevArgs {{ ∀ (n : nat), Proper lp:{{ Sig {{ n }} }} lp:PhiTerm }} :-
  std.rev RevArgs Args,
  PhiTerm = app [Phi | Args],
  (pi n\ mk-ne-sig n NEType (Sig n)).


pred mk-pre-ne.forall i:list param, i:term, i:term, i:term, i:term, i:list term, o:term.
mk-pre-ne.forall Params (prod Name T F) Pre N Rec Args (prod Name T F') :-
  (pi x\ mk-pre-ne.forall Params (F x) Pre N Rec [x | Args] (F' x)).
mk-pre-ne.forall Params NEType Pre N Rec RevArgs {{ (Proper lp:Sig lp:RecTerm) -> Proper lp:Sig lp:PreTerm }} :-
  std.rev RevArgs Args,
  std.map Params (x\r\ x = (par _ _ _ r)) Ps,
  RecTerm = app [Rec | Args],
  PreTerm = app [Pre | {std.append Ps [Rec | Args]}],
  mk-ne-sig N NEType Sig.


pred mk-pre-ne.toproof i:list param, i:term, i:term, i:term, o:term.
mk-pre-ne.toproof Params Type NEType Pre {{ ∀ n (rec : lp:Type), lp:{{ NonExpansiveTerm {{ rec }} }} -> lp:{{ F {{ n }} {{ rec }} }} }} :-
  (pi n rec\ 
    mk-pre-ne.forall Params NEType Pre n rec [] (F n rec),
    mk-ne-phi-ass rec NEType [] (NonExpansiveTerm rec)
  ).


pred mk-pre-ne i:list param, i:term, i:term, i:term, o:hole.
mk-pre-ne Params Type NEType Pre (hole ToProof Proof) :-
  mk-pre-ne.toproof Params Type NEType Pre ToProof'', !,
  replace-params-ty Params ToProof'' ToProof',
  (@reversible! => @primitive! => coq.elaborate-skeleton ToProof' {{ Type }} ToProof D),
  if (D = error Msg) 
    (if-debug (coq.say "Failure in making Pre NonExpansive type ("{coq.term->string ToProof'}"):" Msg), fail) 
    (if-debug (coq.say "Proving Pre Non-Expansive:" {coq.term->string ToProof})), !,
  coq.typecheck Proof ToProof D',
  if (D' = error Msg) 
    (if-debug (coq.say "Failure in making Pre NonExpansive type (Proof:"{coq.term->string ToProof}"):" Msg), fail) 
    (if-debug (coq.say "Proving Pre Non-Expansive:" {coq.term->string ToProof})), !,
  coq.ltac.collect-goals Proof [G] _, !,
  coq.ltac.open solve_proper_ltac G [].


pred ne-to-lam i:term, i:term, i:list term, o:term.
ne-to-lam Phi (prod N T F) Args (fun N T F') :-
  (pi x\ ne-to-lam Phi (F x) [x | Args] (F' x)).
ne-to-lam Phi {{ _ (lp:XType -n> lp:NEType) }} Args {{λ x:lp:XType, lp:{{ Lam {{ x }} }} }} :- !,
  (pi x\ ne-to-lam Phi NEType [x | Args] (Lam x)).
ne-to-lam Phi {{ lp:XType -n> lp:NEType }} Args {{λ x:lp:XType, lp:{{ Lam {{ x }} }} }} :- !,
  (pi x\ ne-to-lam Phi NEType [x | Args] (Lam x)).
ne-to-lam Phi _ RevArgs (app [Phi | Args]) :-
  std.rev RevArgs Args.


pred mk-fixpoint.forall i:term, i:list param, i:term, i:term, i:list term, i:term, o:term.
mk-fixpoint.forall Pre Params NEType Phi Xs (prod N T F) {{ bi_forall lp:Fun }} :-
  Fun = fun N T F',
  pi x\ mk-fixpoint.forall Pre Params NEType Phi [x | Xs] (F x) (F' x).
mk-fixpoint.forall Pre Params NEType Phi RevXs _ {{ bi_wand lp:L lp:R }} :-
  std.rev RevXs Xs,
  std.map Params (x\r\ x = par _ _ _ r) Ps,
  ne-to-lam Phi NEType [] PhiLam,
  L = app { std.append [Pre | Ps] [PhiLam | Xs] },
  R = app [Phi | Xs].


pred mk-fixpoint.fun i:term, i:list param, i:term, i:term, i:term, i:list term, o:term.
mk-fixpoint.fun Pre Params NEType Type (prod N T F) L (fun N T F') :-
  pi x\ mk-fixpoint.fun Pre Params NEType Type (F x) [x | L] (F' x).
mk-fixpoint.fun Pre Params NEType Type _ RevL {{ bi_forall lp:F }} :-
  std.rev RevL L,
  F = fun {coq.string->name "Φ"} NEType Forall,
  pi phi\ ( sigma AppPhi\ sigma Prem\
    AppPhi = app [phi | L],
    mk-fixpoint.forall Pre Params NEType phi [] Type Prem,
    Forall phi = {{ bi_wand (□ lp:Prem) lp:AppPhi }}
  ).


pred mk-fixpoint i:list param, i:term, i:term, i:term, o:term.
mk-fixpoint Params Type NEType Pre Fixpoint :-
  mk-fixpoint.fun Pre Params NEType Type Type [] Fixpoint'',
  replace-params-bo Params Fixpoint'' Fixpoint',
  if-debug (coq.say "----- Fixpoint Body" {coq.term->string Fixpoint'}), !,
  (@reversible! => @primitive! => 
    std.assert-ok! (coq.elaborate-skeleton Fixpoint' _ Fixpoint) "Failed to elaborate the fixpoint").


pred mk-fix-toproof i:(list term -> list term -> term -> prop), i:list param, i:term, i:list term, i:list term, i:term, o:term.
mk-fix-toproof MK Params Pre Fixes Xs (prod N T F) (prod N T F') :-
  @pi-decl N T x\ mk-fix-toproof MK Params Pre Fixes [x | Xs] (F x) (F' x).
mk-fix-toproof MK Params Pre Fixes RevXs _ Res :-
  std.rev RevXs Xs,
  std.map Params (x\r\ x = par _ _ _ r) Ps,
  std.map Fixes (f\r\ sigma App\ std.append [Pre | Ps] [app [f | Ps] | Xs] App, r = app App) Unfolds,
  std.map Fixes (f\r\ sigma App\ std.append Ps Xs App, r = app [f | App]) Folds,
  MK Unfolds Folds Res.



pred mk-fix-ne.forall i:list param, i:term, i:term, i:term, i:list term, o:term.
mk-fix-ne.forall Params (prod Name T F) Fix N Args (prod Name T F') :-
  (pi x\ mk-fix-ne.forall Params (F x) Fix N [x | Args] (F' x)).
mk-fix-ne.forall Params NEType Fix N RevArgs {{ Proper lp:Sig lp:FixTerm }} :-
  std.rev RevArgs Args,
  std.map Params (x\r\ x = (par _ _ _ r)) Ps,
  FixTerm = app [Fix | {std.append Ps Args}],
  mk-ne-sig N NEType Sig.


pred mk-fix-ne.toproof  i:list param, i:term, i:term, o:term.
mk-fix-ne.toproof Params NEType Fix {{ ∀ n, lp:{{ F {{ n }} }} }} :-
  (pi n rec\ mk-fix-ne.forall Params NEType Fix n [] (F n)).


pred mk-fix-ne i:list param, i:term, i:term, o:hole.
mk-fix-ne Params NEType Fix (hole Type Proof) :-
  mk-fix-ne.toproof Params NEType Fix Type'', !,
  replace-params-ty Params Type'' Type',
  (@reversible! => @primitive! => coq.elaborate-skeleton Type' {{ Type }} Type D),
  if (D = error Msg) 
    (if-debug (coq.say "Failure in making Fix NonExpansive type ("{coq.term->string Type}":Type):" Msg), fail) 
    (if-debug (coq.say "Proving Fix Non-Expansive:" {coq.term->string Type})), !,
  coq.typecheck Proof Type D',
  if (D' = error Msg) 
    (if-debug (coq.say "Failure in making Fix NonExpansive type (Proof:"{coq.term->string Type}"):" Msg), fail) 
    (if-debug (coq.say "Proving Fix Non-Expansive:" {coq.term->string Type})), !,
  coq.ltac.collect-goals Proof [G] _, !,
  coq.ltac.open solve_proper_ltac G [].


pred mk-unfold-2.proof i:int, i:int, i:term, i:term, i:hole.
mk-unfold-2.proof Ps N Proper Mono (hole Type Proof) :-
  do-intros-forall (hole Type Proof) (mk-unfold-2.proof-1 Ps N Proper Mono).

pred mk-unfold-2.proof-1 i:int, i:int, i:term, i:term, i:hole.
mk-unfold-2.proof-1 Ps N Proper Mono H :-
  do-iStartProof H IH, !,
  if-debug (coq.say "started proof unfold_2" {ihole->string IH}),
  do-iIntros [iIdent (iNamed "HF"), iPure none, 
              iIntuitionistic (iIdent (iNamed "HI")), 
              iHyp "HI"] IH (mk-unfold-2.proof-2 Ps N Proper Mono).

pred mk-unfold-2.proof-2 i:int, i:int, i:term, i:term, i:ihole.
mk-unfold-2.proof-2 Ps N Proper Mono IH :-
  % std.map {std.iota Ps} (x\r\ r = {{ _ }}) Holes, !,
  % do-iApplyLem (app [Proper | Holes]) IH [] [IH1, IH2],
  ((copy {{ @IProper }} {{ @iProper }} :- !) => copy Mono MonoiProper'),
  type-to-fun MonoiProper' MonoiProper,
  std.map {std.iota Ps} (x\r\ r = {{ _ }}) Holes, !,
  do-iApplyLem (app [MonoiProper | Holes]) IH [(h\ sigma PType\ sigma PProof\ sigma List\ sigma Holes2\ !,
    h = hole PType PProof,
    std.iota Ps List,
    std.map List (x\r\ r = {{ _ }}) Holes2,
    coq.elaborate-skeleton (app [Proper | Holes2]) PType PProof D,
    if (D = ok) (true) (if-debug (coq.say "IProper error" D), fail)
  )] [IH1, IH2],
  if-debug (coq.say "pre apply HF" {ihole->string IH2}), !,
  do-iApplyHyp "HF" IH2 [], !,
  std.map {std.iota N} (x\r\ r = iPure none) Pures, !,
  do-iIntros {std.append [iModalIntro | Pures] [iIdent (iNamed "H"), iHyp "H", iModalIntro, iHyp "HI"]} IH1 (ih\ true).

pred mk-unfold-2 i:list param, i:term, i:term, i:term, i:term, i:term, o:hole.
mk-unfold-2 Params Pre Proper Mono Fix Type (hole ToProof Proof) :-
  mk-fix-toproof (u\f\res\ sigma U\ sigma F\ u = [U], f = [F], res = {{ lp:U ⊢ lp:F }}) Params Pre [Fix] [] Type ToProof', !,
  replace-params-ty Params ToProof' ToProof, !,
  if-debug (coq.say "unfold 2:" {coq.term->string ToProof}),
  mk-unfold-2.proof {std.length Params} {type-depth Type} Proper Mono (hole ToProof Proof).


pred mk-unfold-1.proof i:list param, i:int, i:term, i:term, i:term, i:term, i:term, i:term, i:hole.
mk-unfold-1.proof Params N NEType Pre Fix Unfold2 Proper Mono (hole Type Proof) :-
  do-intros-forall (hole Type Proof) (mk-unfold-1.proof-1 Params N NEType Pre Fix Unfold2 Proper Mono), !,
  if-debug (coq.say "Only solve_proper is left"),
  coq.ltac.collect-goals Proof GS SGS, !,
  std.forall GS (g\
    if-debug (coq.say "Found the proper goal", coq.ltac.open show-goal g _),
    coq.ltac.open solve_proper_ltac g []
  ), !,
  std.forall SGS (g\
    if-debug (coq.say "Found the shelved proper goal", coq.ltac.open show-goal g _),
    coq.ltac.open solve_proper_ltac g []
  ).

pred mk-unfold-1.proof-1 i:list param, i:int, i:term, i:term, i:term, i:term, i:term, i:term, i:hole.
mk-unfold-1.proof-1 Params N NEType Pre Fix Unfold2 Proper Mono H :-
  do-iStartProof H IH, !,
  if-debug (coq.say "started proof unfold_1" {ihole->string IH}),
  std.map {std.iota N} (x\r\ r = iPure none) Pures, !,
  do-iIntro-ident (iNamed "HF") IH IH',
  % Generate argument to specialize with
  mk-unfold-1.proof-1.aux Pre Fix Params NEType [] SpecTerm,
  % TODO: Change params to introduced versions
  if-debug (coq.say "Specialization term" {coq.term->string SpecTerm}), !,
  % Specialize
  do-iSpecializeForall IH' (iNamed "HF") SpecTerm IH'',
  do-iIntros { std.append [iHyp "HF", iModalIntro | Pures]
                          [iIdent (iNamed "Hy")] } 
              IH'' (mk-unfold-1.proof-2 {std.length Params} N Unfold2 Proper Mono).

mk-unfold-1.proof-1.aux Pre Fix Params (prod N T F) L (fun N T F') :- !,
  (pi x\ mk-unfold-1.proof-1.aux Pre Fix Params (F x) [x | L] (F' x)).
mk-unfold-1.proof-1.aux Pre Fix Params {{ _ (_ -n> lp:NEType) }} L {{ λne x, lp:{{ OfeTerm {{ x }} }} }} :-
  (pi x\ mk-unfold-1.proof-1.aux Pre Fix Params NEType [x | L] (OfeTerm x)).
mk-unfold-1.proof-1.aux Pre Fix Params {{ _ -n> lp:NEType }} L {{ λne x, lp:{{ OfeTerm {{ x }} }} }} :-
  (pi x\ mk-unfold-1.proof-1.aux Pre Fix Params NEType [x | L] (OfeTerm x)).
mk-unfold-1.proof-1.aux Pre Fix Params _ RevL PreTerm :- !,
  std.rev RevL L,
  std.map Params (x\r\ x = par _ _ _ r) ParamTerms,
  PreTerm = app { std.append { std.append [Pre | ParamTerms] [app [Fix | ParamTerms]] } L }.

              
pred mk-unfold-1.proof-2 i:int, i:int, i:term, i:term, i:term, i:ihole.
mk-unfold-1.proof-2 Ps N Unfold2 Proper Mono IH :-
  ((copy {{ @IProper }} {{ @iProper }} :- !) => copy Mono MonoiProper'),
  type-to-fun MonoiProper' MonoiProper,
  std.map {std.iota Ps} (x\r\ r = {{ _ }}) Holes, !,
  do-iApplyLem (app [MonoiProper | Holes]) IH [(h\ sigma PType\ sigma PProof\ sigma List\ sigma Holes2\ !,
    h = hole PType PProof,
    std.iota Ps List,
    std.map List (x\r\ r = {{ _ }}) Holes2,
    coq.elaborate-skeleton (app [Proper | Holes2]) PType PProof D,
    if (D = ok) (true) (if-debug (coq.say "IProper error" D), fail)
  )] [IH1, IH2],
  do-iApplyHyp "Hy" IH2 [], !,
  std.map {std.iota N} (x\r\ r = iPure none) Pures, !,
  do-iIntros {std.append [iModalIntro | Pures] [iIdent (iNamed "HF")]} IH1 (mk-unfold-1.proof-3 Ps Unfold2).

pred mk-unfold-1.proof-3 i:int, i:term, i:ihole.
mk-unfold-1.proof-3 Ps Unfold2 IH :-
  std.map {std.iota Ps} (x\r\ r = {{ _ }}) Holes, !,
  do-iApplyLem (app [Unfold2 | Holes]) IH [] [AppliedIH], !,
  do-iApplyHyp "HF" AppliedIH [].

pred mk-unfold-1 i:list param, i:term, i:term, i:term, i:term, i:term , i:term, i:term, o:hole.
mk-unfold-1 Params Unfold2 Pre Proper Mono Fix Type NEType (hole ToProof Proof) :-
  mk-fix-toproof (u\f\res\ sigma U\ sigma F\ u = [U], f = [F], res = {{ lp:F ⊢ lp:U }}) Params Pre [Fix] [] Type ToProofNoParams', !,
  coq.elaborate-skeleton ToProofNoParams' {{ Type }} ToProofNoParams D, !,
  if (D = error Msg) 
    (if-debug (coq.say "Failure in making Unfold1 type ("{coq.term->string ToProofNoParams}":Type):" Msg), fail) 
    (if-debug (coq.say "Proving Unfold1:" {coq.term->string ToProofNoParams})), !,
  mk-unfold-1.proof Params {type-depth Type} NEType Pre Fix Unfold2 Proper Mono (hole ToProofNoParams ProofNoPramas),
  replace-params-ty Params ToProofNoParams ToProof,!,
  replace-params-bo Params ProofNoPramas Proof,
  if-debug (coq.say "unfold 1 done").


pred mk-unfold.proof i:int, i:term, i:term, i:hole.
mk-unfold.proof Ps Unfold1 Unfold2 (hole Type Proof) :-
  do-intros-forall (hole Type Proof) (mk-unfold.proof-1 Ps Unfold1 Unfold2).

pred mk-unfold.proof-1 i:int, i:term, i:term, i:hole.
mk-unfold.proof-1 Ps Unfold1 Unfold2 H :-
  do-iStartProof H (ihole N H'), !,
  if-debug (coq.say "Started proof" {ihole->string (ihole N H')}),
  do-iAndSplit H' H1 H2,
  if-debug (coq.say "H1:" {ihole->string (ihole N H1)}),
  if-debug (coq.say "H2:" {ihole->string (ihole N H2)}),
  std.map {std.iota Ps} (x\r\ r = {{ _ }}) Holes1, !,
  do-iApplyLem (app [Unfold1 | Holes1]) (ihole N H1) [] [], !,
  std.map {std.iota Ps} (x\r\ r = {{ _ }}) Holes2, !,
  do-iApplyLem (app [Unfold2 | Holes2]) (ihole N H2) [] [].

pred mk-unfold i:list param, i:term, i:term, i:term, i:term, i:term, o:hole.
mk-unfold Params Unfold1 Unfold2 Pre Fix Type (hole ToProof Proof) :-
  mk-fix-toproof (u\f\res\ sigma U\ sigma F\ u = [U], f = [F], res = {{ lp:F ⊣⊢ lp:U }}) Params Pre [Fix] [] Type ToProof', !,
  replace-params-ty Params ToProof' ToProof,!,
  if-debug (coq.say "unfold:" {coq.term->string ToProof}),
  mk-unfold.proof {std.length Params} Unfold1 Unfold2 (hole ToProof Proof),
  if-debug (coq.say "unfold done").


pred constr-in-prebody i:term, i:list term, i:option term, i:list term, i:int, i:int, o:term.
constr-in-prebody Pre Ps Fix Xs Nth Total Res :- 
  if-debug (coq.say "constr-in-prebody" {coq.term->string Pre} Ps Fix Xs Nth Total Res), fail.
constr-in-prebody (fun _ _ F) [P | Ps] Fix Xs Nth Total Res :- !,
  constr-in-prebody (F P) Ps Fix Xs Nth Total Res.
constr-in-prebody (fun _ _ F) [] (some Fix) Xs Nth Total Res :- !,
  constr-in-prebody (F Fix) [] none Xs Nth Total Res.
constr-in-prebody (fun _ _ F) [] none [X | Xs] Nth Total Res :- !,
  constr-in-prebody (F X) [] none Xs Nth Total Res.
constr-in-prebody T [] none [] 0 1 T :- !.
constr-in-prebody (app [_, _, _, R]) [] none [] 1 2 R :- !.
constr-in-prebody (app [_, _, L, _]) [] none [] 0 _ L :- !.
constr-in-prebody (app [_, _, _, R]) [] none [] Nth Total Res :- !,
  constr-in-prebody R [] none [] { calc (Nth - 1) } { calc (Total - 1) } Res.


pred mk-constr-lem.toproof i:list param, i:term, i:term, i:term, i:int, i:int, i:list term, i:list term, o:term.
mk-constr-lem.toproof [(par ID _ T C) | Params] PreBody Fix Type Nth Total Ps [] (prod N T F) :- !,
  coq.id->name ID N,
  mk-constr-lem.toproof Params PreBody Fix Type Nth Total [C | Ps] [] Lem,
  pi x\ (copy C x :- !) => copy Lem (F x).
mk-constr-lem.toproof [] PreBody Fix (prod N T F) Nth Total Ps Xs (prod N T F') :- !,
  @pi-decl N T x\ mk-constr-lem.toproof [] PreBody Fix (F x) Nth Total Ps [x | Xs] (F' x).
mk-constr-lem.toproof [] PreBody Fix _ Nth Total PsRev XsRev {{ lp:Constr -∗ lp:FixTerm }} :- !,
  std.rev PsRev Ps,
  std.rev XsRev Xs,
  constr-in-prebody PreBody Ps (some (app [Fix | Ps])) Xs Nth Total Constr,
  FixTerm = app {std.append [Fix | Ps] Xs}.

pred mk-constr-lem.proof i:term, i:int, i:int, i:int, i:hole.
mk-constr-lem.proof Unfold2 Ps Nth Total (hole Type Proof) :-
  do-intros-forall (hole Type Proof) (mk-constr-lem.proof-1 Unfold2 Ps Nth Total).

pred mk-constr-lem.proof-1 i:term, i:int, i:int, i:int, i:hole.
mk-constr-lem.proof-1 Unfold2 Ps Nth Total H :-
  do-iStartProof H IH, !,
  do-iIntros [iIdent (iNamed "H")] IH (mk-constr-lem.proof-2 Unfold2 Ps Nth Total).

pred mk-constr-lem.proof-2 i:term, i:int, i:int, i:int, i:ihole.
mk-constr-lem.proof-2 Unfold2 Ps Nth Total IH :-
  std.map {std.iota Ps} (x\r\ r = {{ _ }}) Holes, !,
  do-iApplyLem (app [Unfold2 | Holes]) IH [] [AppliedIH], !,
  mk-constr-lem.proof-3 Nth Total AppliedIH.

pred mk-constr-lem.proof-3 i:int, i:int, i:ihole.
mk-constr-lem.proof-3 Nth Total IH :- if-debug (coq.say "mk-constr-lem.proof-3" Nth Total {ihole->string IH}), fail.
mk-constr-lem.proof-3 0 1 IH :- !,
  do-iApplyHyp "H" IH [].
mk-constr-lem.proof-3 1 2 (ihole N H) :- !,
  do-iRight H H', !,
  mk-constr-lem.proof-3 0 1 (ihole N H').
mk-constr-lem.proof-3 0 _ (ihole N H) :- !,
  do-iLeft H H', !,
  mk-constr-lem.proof-3 0 1 (ihole N H').
mk-constr-lem.proof-3 Nth Total (ihole N H) :- !,
  do-iRight H H', !,
  mk-constr-lem.proof-3 { calc (Nth - 1) } {calc (Total - 1)} (ihole N H').

pred mk-constr-lem i:list param, i:term, i:(term -> list indc-decl), i:term, i:term, i:term, i:int, i:int, o:constant.
mk-constr-lem Params Unfold2 Constructors PreBody Fix Type Nth Total ConstrLem :-
  mk-constr-lem.toproof Params PreBody Fix Type Nth Total [] [] ConstrType,
  if-debug (coq.say "----- Constr Lemma" { coq.term->string ConstrType } ),
  mk-constr-lem.proof Unfold2 {std.length Params} Nth Total (hole ConstrType ConstrProof),
  
  std.nth Nth (Constructors Fix) (constructor Name _),
  coq.env.add-const Name ConstrProof ConstrType ff ConstrLem.


pred mk-iter.toproof.inner-1 i:list param, i:term, i:term, i:list term, i:term, o:term.
mk-iter.toproof.inner-1 Params Pre Phi Xs (prod N T F) {{ bi_forall lp:Fun }} :-
  Fun = fun N T F',
  @pi-decl N T x\ mk-iter.toproof.inner-1 Params Pre Phi [x | Xs] (F x) (F' x).
mk-iter.toproof.inner-1 Params Pre Phi RevXs _ {{ bi_wand lp:L lp:R }} :-
  std.rev RevXs Xs,
  std.map Params (x\r\ x = par _ _ _ r) Ps,
  L = app {std.append [Pre | Ps] [Phi | Xs]},
  R = app [Phi | Xs].

pred mk-iter.toproof.inner-2 i:list param, i:term, i:term, i:list term, i:term, o:term.
mk-iter.toproof.inner-2 Params Fix Phi Xs (prod N T F) {{ bi_forall lp:Fun }} :-
  Fun = fun N T F',
  @pi-decl N T x\ mk-iter.toproof.inner-2 Params Fix Phi [x | Xs] (F x) (F' x).
mk-iter.toproof.inner-2 Params Fix Phi RevXs _ {{ bi_wand lp:L lp:R }} :-
  std.rev RevXs Xs,
  std.map Params (x\r\ x = par _ _ _ r) Ps,
  L = app {std.append [Fix | Ps] Xs},
  R = app [Phi | Xs].


pred mk-iter.toproof i:list param, i:term, i:term, i:term, i:term, o:term.
mk-iter.toproof Params Pre Fix Type NEType ToProof :-
  (pi phi\ 
    mk-iter.toproof-2 Params Pre Fix Type phi (F phi),
    if (Type = NEType)
      (ToProof = {{ ∀ (Φ : lp:Type), lp:{{ F {{ Φ }}}} }})
      (
        mk-ne-phi-ass phi NEType [] (NonExpansiveTerm phi),
        ToProof = {{ ∀ (Φ : lp:Type), lp:{{ NonExpansiveTerm {{ Φ }} }} -> lp:{{ F {{ Φ }}}} }}
      )
  ).

pred mk-iter.toproof-2 i:list param, i:term, i:term, i:term, i:term, o:term.
mk-iter.toproof-2 Params Pre Fix Type Phi {{ (□ lp:Assump) -∗ lp:Prem }} :-
  mk-iter.toproof.inner-1 Params Pre Phi [] Type Assump,
  mk-iter.toproof.inner-2 Params Fix Phi [] Type Prem.

pred mk-iter.proof i:list param, i:prop, i:term, i:int, i:hole.
mk-iter.proof Params HasNE NEType N (hole Type Proof) :-
  do-intros-forall-remember (hole Type Proof) (mk-iter.proof-1 Params HasNE NEType N),
  if-debug (coq.say "Only solve_proper is left"),
  coq.ltac.collect-goals Proof GS SGS, !,
  std.forall GS (g\
    if-debug (coq.say "Found the proper goal", coq.ltac.open show-goal g _),
    coq.ltac.open solve_proper_ltac g []
  ), !,
  std.forall SGS (g\
    if-debug (coq.say "Found the shelved proper goal", coq.ltac.open show-goal g _),
    coq.ltac.open solve_proper_ltac g []
  ).

pred mk-iter.proof-1 i:list param, i:prop, i:term, i:int, i:list term, i:hole.
mk-iter.proof-1 Params HasNE NEType N Args H :-
  do-iStartProof H IH, !,
  std.map { std.iota N } (x\r\ r = iPure none) Pures, !,
  do-iIntros {std.append [iIntuitionistic (iIdent (iNamed "Hphi")) | Pures] [iIdent (iNamed "HF")]}
             IH (mk-iter.proof-2 Params HasNE NEType Args).

pred mk-iter.proof-2 i:list param, i:prop, i:term, i:list term, i:ihole.
mk-iter.proof-2 Params HasNE NEType Args IH :- 
  if (HasNE)
    (std.rev Args [Phi | ParamTerms])
    (std.rev Args [_, Phi | ParamTerms]),
  replace-params-terms Params {std.rev ParamTerms} NEType NEType',
  mk-iter.proof-2.aux Phi NEType' [] SpecTerm,
  if-debug (coq.say "Specialization term" {coq.term->string SpecTerm}), !,
  do-iSpecializeForall IH (iNamed "HF") SpecTerm IH',
  do-iIntros [iHyp "HF", iHyp "Hphi"] IH' (ih\ true).


pred replace-params-terms i:list param, i:list term, i:term, o:term.
replace-params-terms Params RTerms ITerm OTerm :-
  std.map Params (x\r\ x = par _ _ _ r) ParamBinders,
  std.map2 ParamBinders RTerms (p\t\r\ r = (copy p t)) Copies,
  (Copies => copy ITerm OTerm).


mk-iter.proof-2.aux Phi (prod N T F) L (fun N T F') :- !,
  (pi x\ if-debug (coq.say "aux prod" {coq.term->string (F x)}), mk-iter.proof-2.aux Phi (F x) [x | L] (F' x)).
mk-iter.proof-2.aux Phi {{ _ ( _ -n> lp:NEType ) }} L {{ λne x, lp:{{ OfeTerm {{ x }} }} }} :- !,
  (pi x\ if-debug (coq.say "aux ne" {coq.term->string NEType}), mk-iter.proof-2.aux Phi NEType [x | L] (OfeTerm x)).
mk-iter.proof-2.aux Phi {{ _ -n> lp:NEType }} L {{ λne x, lp:{{ OfeTerm {{ x }} }} }} :- !,
  (pi x\ if-debug (coq.say "aux ne2" {coq.term->string NEType}), mk-iter.proof-2.aux Phi NEType [x | L] (OfeTerm x)).
mk-iter.proof-2.aux Phi Base RevL (app [Phi | L]) :- !,
  if-debug (coq.say "aux base" {coq.term->string Base}),
  std.rev RevL L.


pred mk-iter i:list param, i:term, i:term, i:term, i:term, o:hole.
mk-iter Params Pre Fix Type NEType (hole ToProof Proof) :- 
  mk-iter.toproof Params Pre Fix Type NEType ToProof', !,
  replace-params-ty Params ToProof' ToProof'', !,
  if-debug (coq.say "iter to proof" {coq.term->string ToProof''}), !,
  coq.elaborate-skeleton ToProof'' {{ Type }} ToProof D,
  if (D = error Msg) 
    (if-debug (coq.say "Failure in making iter proof type ("{coq.term->string ToProof}":Type):" Msg), fail) 
    (true), !,
  mk-iter.proof Params (Type = NEType) NEType {type-depth Type} (hole ToProof Proof),
  if-debug (coq.say "iter proof finished").


pred mk-ind.toproof i:list param, i:term, i:term, i:term, i:term, o:term.
mk-ind.toproof Params Pre Fix Type NEType ToProof :-
  (pi phi\ 
    mk-ind.toproof-2 Params Pre Fix Type phi (F phi),
    if (Type = NEType)
      (ToProof = {{ ∀ (Φ : lp:Type), lp:{{ F {{ Φ }}}} }})
      (
        mk-ne-phi-ass phi NEType [] (NonExpansiveTerm phi),
        ToProof = {{ ∀ (Φ : lp:Type), lp:{{ NonExpansiveTerm {{ Φ }} }} -> lp:{{ F {{ Φ }}}} }}
      )
  ).

pred mk-ind.toproof-2 i:list param, i:term, i:term, i:term, i:term, o:term.
mk-ind.toproof-2 Params Pre Fix Type Phi {{ (□ lp:L) -∗ lp:R }} :-
  mk-ind.toproof-inner Params Pre Fix Phi [] Type Type L,
  mk-iter.toproof.inner-2 Params Fix Phi [] Type R.

pred mk-ind.toproof-fun i:list param, i:term, i:term, i:list term, i:term, o:term.
mk-ind.toproof-fun Params Phi Fix Xs (prod N T F) (fun N T F') :-
  pi x\ mk-ind.toproof-fun Params Phi Fix [x | Xs] (F x) (F' x).
mk-ind.toproof-fun Params Phi Fix RevXs _ Res :-
  std.rev RevXs Xs,
  std.map Params (x\r\ x = par _ _ _ r) Ps,
  L = app [Phi | Xs],
  R = app [Fix | {std.append Ps Xs}],
  Res = {{ bi_and lp:L lp:R }}.

pred mk-ind.toproof-inner i:list param, i:term, i:term, i:term, i:list term, i:term, i:term, o:term.
mk-ind.toproof-inner Params Pre Fix Phi Xs Type (prod N T F) {{ bi_forall lp:Fun }} :-
  Fun = fun N T F',
  @pi-decl N T x\ mk-ind.toproof-inner Params Pre Fix Phi [x | Xs] Type (F x) (F' x).
mk-ind.toproof-inner Params Pre Fix Phi RevXs Type _ {{ bi_wand lp:L lp:R }} :-
  std.rev RevXs Xs,
  std.map Params (x\r\ x = par _ _ _ r) Ps,
  mk-ind.toproof-fun Params Phi Fix [] Type IH,
  L = app {std.append [Pre | Ps] [IH | Xs]},
  R = app [Phi | Xs].

pred mk-ind.proof i:int, i:int, i:term, i:term, i:term, i:term, i:term, i:hole.
mk-ind.proof Ps N Unfold1 Unfold2 Proper Mono Iter (hole Type Proof) :-
  do-intros-forall (hole Type Proof) (mk-ind.proof-1 Ps N Unfold1 Unfold2 Proper Mono Iter),
  if-debug (coq.say "Only solve_proper is left"),
  coq.ltac.collect-goals Proof GS SGS, !,
  std.forall GS (g\
    if-debug (coq.say "Found the proper goal", coq.ltac.open show-goal g _),
    coq.ltac.open solve_proper_ltac g []
  ), !,
  std.forall SGS (g\
    if-debug (coq.say "Found the shelved proper goal", coq.ltac.open show-goal g _),
    coq.ltac.open solve_proper_ltac g []
  ).

pred mk-ind.proof-1 i:int, i:int, i:term, i:term, i:term, i:term, i:term i:hole.
mk-ind.proof-1 Ps N Unfold1 Unfold2 Proper Mono Iter H :-
  do-iStartProof H IH, !,
  std.map {std.iota N} (x\r\ r = iPure none) Pures, !,
  do-iIntros {std.append [iIntuitionistic (iIdent (iNamed "Hmon")) | Pures] 
                          [iIdent (iNamed "Hx"), iHyp "Hmon"]} IH (mk-ind.proof-2 Ps N Unfold1 Unfold2 Proper Mono Iter).

pred mk-ind.proof-2 i:int, i:int, i:term, i:term, i:term, i:term, i:term, i:ihole.
mk-ind.proof-2 Ps N Unfold1 Unfold2 Proper Mono Iter (ihole IN H) :-
  do-iEvalIn (iNamed "Hx") H IH H1,
  do-iApplyLem Unfold1 IH [] [], !,
  % Apply Monotinicity
  ((copy {{ @IProper }} {{ @iProper }} :- !) => copy Mono MonoiProper'),
  type-to-fun MonoiProper' MonoiProper,
  std.map {std.iota Ps} (x\r\ r = {{ _ }}) Holes, !,
  do-iApplyLem (app [MonoiProper | Holes]) (ihole IN H1) [(h\ sigma PType\ sigma PProof\ sigma List\ sigma Holes'\ sigma PTypeString\ !,
    h = hole PType PProof,
    std.iota Ps List,
    std.map List (x\r\ r = {{ _ }}) Holes',
    coq.elaborate-skeleton (app [Proper | Holes']) PType PProof D,
    if (D = ok) (true) (if-debug (coq.say "IProper error" D), fail)
  )] [(ihole N2 H2), IH2], !,
  do-iApplyHyp "Hx" IH2 [], !,
  % Continue proof
  do-iModIntro H2 H3,
  do-iApplyLem Iter (ihole N2 H3) [] [IH3],
  std.map {std.iota N} (x\r\ r = iPure none) Pures, !,
  do-iIntros {std.append [iModalIntro | Pures] [iIdent (iNamed "Hilp")]} IH3 (mk-ind.proof-3 Ps N Unfold2 Proper Mono).

pred mk-ind.proof-3 i:int, i:int, i:term, i:term, i:term, i:ihole.
mk-ind.proof-3 Ps N Unfold2 Proper Mono (ihole IN H) :-
  do-iAndSplit H H1 H2,!,
  % Left branch
  do-iApplyHyp "Hmon" (ihole IN H1) [IH1],!,
  do-iApplyHyp "Hilp" IH1 [],!,
  % Right branch
  do-iApplyLem Unfold2 (ihole IN H2) [] [IH2],!,
  % Apply Mono
  ((copy {{ @IProper }} {{ @iProper }} :- !) => copy Mono MonoiProper'),
  type-to-fun MonoiProper' MonoiProper,
  std.map {std.iota Ps} (x\r\ r = {{ _ }}) Holes, !,
  do-iApplyLem (app [MonoiProper | Holes]) IH2 [(h\ sigma PType\ sigma PProof\ sigma List\ sigma Holes'\ !,
    h = hole PType PProof,
    std.iota Ps List,
    std.map List (x\r\ r = {{ _ }}) Holes',
    coq.elaborate-skeleton (app [Proper | Holes']) PType PProof D,
    if (D = ok) (true) (if-debug (coq.say "IProper error" D), fail)
  )] [IH3, IH4],!,
  do-iApplyHyp "Hilp" IH4 [], !,
  std.map {std.iota N} (x\r\ r = iPure none) Pures, !,
  do-iIntros {std.append [iModalIntro | Pures] [iList [[iDrop, iIdent (iNamed "Hilp")]], iHyp "Hilp"]} IH3 (ih\ true).

pred mk-ind i:list param, i:term, i:term, i:term, i:term, i:term, i:term, i:term, i:term, i:term, o:hole.
mk-ind Params Pre Fix Unfold1 Unfold2 Proper Mono Iter Type NEType (hole ToProof Proof) :-
  mk-ind.toproof Params Pre Fix Type NEType ToProof',
  replace-params-ty Params ToProof' ToProof'', !,
  if-debug (coq.say "ind to proof" {coq.term->string ToProof''}), !,
  coq.elaborate-skeleton ToProof'' {{ Type }} ToProof D,
  if (D = error Msg) 
    (if-debug (coq.say "Failure in making ind proof type ("{coq.term->string ToProof}":Type):" Msg), fail) 
    (true), !,
  mk-ind.proof {std.length Params} {type-depth Type} Unfold1 Unfold2 Proper Mono Iter (hole ToProof Proof), !,
  if-debug (coq.say "ind proof finished").