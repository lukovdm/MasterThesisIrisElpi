pred foldr i:list B, i:A, i:(B -> A -> A -> prop), o:A.
foldr [] A _ A.
foldr [X|XS] A F R :- foldr XS A F A', F X A' R.

pred forall2-flex i:list A, i:list B, i:(A -> B -> prop).
forall2-flex [X|XS] [Y|YS] P :- P X Y, !, forall2-flex XS YS P.
forall2-flex _ _ _.

pred int->bools i:int, o:list bool.
pred int->bools.unpadded i:int, o:list bool.
int->bools.unpadded 0 [] :- !.
int->bools.unpadded INT BITS :-
  if ({ calc (INT mod 2) } == 1) (BIT is tt) (BIT is ff),
  BITS = [BIT | { int->bools.unpadded { calc (INT div 2) } }].
int->bools INT BITS :-
  int->bools.unpadded INT BITSSHORT,
  std.length BITSSHORT SIZE,
  std.map { std.iota { calc  (8 - SIZE) } } (x\ r\ r = ff) PADDING,
  std.append BITSSHORT PADDING BITS.

pred string->ascii i:string, o:list (list bool).
string->ascii.aux INDEX STRING BS :- 
  INT is rhc (substring STRING INDEX 1),
  int->bools INT BS.
string->ascii S BSS :-
  LENGTH is size S,
  std.map {std.iota LENGTH} (x\ r\ string->ascii.aux x S r) BSS.

pred string->stringterm i:string, o:term.
pred string->stringterm.aux i:list bool, i:term, o:term.
string->stringterm.aux X ACC RES :-
  std.map X (x\ r\ if (x == tt) (r = {{ true }}) (r = {{ false }})) BITSTERM,
  RES = app [{{ String }}, app [{{ Ascii }} | BITSTERM], ACC].
string->stringterm STRING T :-
  string->ascii STRING BSS,
  foldr BSS {{ EmptyString }} string->stringterm.aux T.

pred list->listterm i:list term, o:term.
list->listterm TS T :- foldr TS {{ [] }} (x\ a\ r\ r = {{ cons lp:x lp:a }}) T.

pred take-while-split i:list A, i:(A -> prop), o:list A, o:list A.
take-while-split [X|XS] Pred [X|YS] ZS :- Pred X, !, take-while-split XS Pred YS ZS.
take-while-split XS _ [] XS.

pred is-digit i:string.
is-digit S :- 1 is size S, C is rhc S, C >= 48, C =< 57.

pred is-identifier i:string.
is-identifier S :- 1 is size S, C is rhc S, ((C >= 48, C =< 57); (C >= 65, C =< 90); (C >= 97, C =< 122); C = 39; C = 95).

pred if-debug i:prop.
if-debug P :- get-option "debug" tt, get-option "start" Start, !, 
  T is {gettimeofday} - Start, 
  coq.say "\n================= [" T "s] =================", 
  P.
if-debug P :- get-option "debug" tt, !, P.
if-debug _ .

pred print-contructors i:list indc-decl.
print-contructors [].
print-contructors [constructor Name Arity | CS] :-
  coq.say "Printing constructor" Name,
  coq.say "of type" { coq.term->string { coq.arity->term Arity } },
  print-contructors CS.

pred map-goal i:(term -> term -> prop), i:goal, o:list sealed-goal.
map-goal P (goal _ _ Type _ _ as G) GL :-
  P Type Type',
  refine {{ _ : lp:Type' }} G GL.

type show-goal open-tactic.
show-goal (goal Ctx _Trigger Type Proof _ as G) [seal G] :-
  coq.say "Goal:" {coq.term->string Proof} ":" {coq.term->string Type} "(" Ctx ")".

pred goal->string o:string, i:goal, o:list sealed-goal.
goal->string S (goal _ _ Type Proof _ as G) [seal G] :-
  S is "Goal: " ^ {coq.term->string Proof} ^ " : " ^ {coq.term->string Type}.

pred type-depth i:term, o:int.
type-depth (prod _ _ F) N :- !,
  (pi x\ type-depth (F x) N'),
  N is N' + 1.
type-depth _ 0.

% Holes are the main type passed around in tactics whenever possible
kind hole type.
type hole term -> term -> hole. % A pair of of Type, Proof

kind ihole type.
type ihole term -> hole -> ihole. % the anonymous iris hyp counter and the hole

pred ihole->string i:ihole, o:string.
ihole->string (ihole N (hole T P)) S :-
  S is {coq.term->string N} ^ " âŠ¢ " ^ {coq.term->string P} ^ " (" ^ {std.any->string P} ^ ") : \n" ^ {coq.term->string T}.

% Handle params and remove them when neccesary
kind param type.
type par id -> implicit_kind -> term -> term -> param.

pred replace-params-bo i:list param, i:term, o:term.
replace-params-bo [] T T.
replace-params-bo [(par ID _IK Type C) | Params] Term (fun N Type FTerm) :-
  coq.id->name ID N,
  replace-params-bo Params Term Term',
  (pi x\ (copy C x :- !) => copy Term' (FTerm x)).

pred replace-params-ty i:list param, i:term, o:term.
replace-params-ty [] T T.
replace-params-ty [(par ID _IK PType C) | Params] Type (prod N PType FType) :-
  coq.id->name ID N,
  replace-params-ty Params Type Type',
  (pi x\ (copy C x :- !) => copy Type' (FType x)).

:before "default-debug-print"
std.debug-print Msg Data :- if-debug (coq.say Msg Data).
