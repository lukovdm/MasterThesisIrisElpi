\documentclass[thesis.tex]{subfiles}

\ifSubfilesClassLoaded{
  \externaldocument{thesis}
  \setcounter{chapter}{4}
}{}

\begin{document}
\VerbatimFootnotes

\chapter{Elpi implementation of Inductive}\label{ch:inductiveimpl}
We discuss the implementation of the \coqi{eiInd} command together with integrations in the \coqi{eiIntros} tactic and the \coqi{eiInduction} tactic.

\paragraph{Structure of \coqii{eiInd}}
The \coqi{eiInd} command consists of several steps we have outlined in \cref{fig:eiinddiagram}. Each of these steps are explained in the sections referenced in the diagram.

\begin{figure}[th!]
  \centering
  \begin{tikzpicture}[node distance=1cm and 1cm]

    \pgfdeclarelayer{foreground}
    \pgfsetlayers{background,main,foreground}

    % Nodes
    \node[obj,e,shortdown] (inddecl) {indt-decl};

    \node[alg={Interpret inductive}, sec={\labelcref{sec:pff},\labelcref{sec:parameters}}, below=of inddecl] (int) {};
    \node[obj, e, below=of int] (typeterm) {Type term};
    \node[obj, right=of typeterm] (params) {Params};
    \node[obj, right=of params] (constructors) {Constructors};

    \begin{scope}[local bounding box=bbox]
      \node[alg={Construct pre \\ fixpoint function}, sec={\labelcref{sec:pff}}, below right=1.5cm and .5cm of typeterm] (createpff) {};
      \node[obj, ce, below=of createpff] (pff) {Pre fixpoint function};

      \node[alg={Construct \& prove \\ signature}, sec={\labelcref{sec:constrsig}}, below left=of pff] (sigalg) {};
      \node[obj, ce, below=of sigalg] (signproff) {Signature with proof};

      \node[alg={Construct fixpoint}, sec={\labelcref{sec:constrfixpoint}}, below right=of pff] (genfixpoint) {};
      \node[obj, ce, below=of genfixpoint] (fixpoint) {Fixpoint};

      \node[alg={Construct \& prove \\ fixpoint properties}, sec={\labelcref{sec:unfolding},\labelcref{sec:constriterind}}, below=of signproff] (fpp) {};
      \node[obj, ce, below=of fpp] (unfolding) {Unfolding \\ property};
      \node[obj, ce, right=of unfolding] (iter) {Iteration \\ property};

      \node[alg={Construct \& prove \\ constructor lemmas}, sec={\labelcref{sec:constrconstr}}, below=of fixpoint] (genconstrlem) {};
      \node[obj, ce, below=of genconstrlem] (constrlem) {Constructor \\ lemmas};

      \node[alg={Construct \& prove \\ induction lemma}, sec={\labelcref{sec:constriterind}}, below right=1cm and .5cm of iter] (genind) {};
      \node[obj, ce, below=of genind] (ind) {Induction lemma\hspace{2pt}};
    \end{scope}

    \node[draw,dashed,thick,rounded corners,fit=(bbox) ($(constructors)!.5!(createpff)$)] (box) {};

    \begin{scope}[on background layer]
      \path[draw, rounded corners]
      (inddecl) -- (int)

      (int) -> (typeterm.north)
      (int) edge[->,rldown] (constructors)
      (int) edge[->,rldown] (params)

      (typeterm) edge[->] (box.north-|typeterm)
      (params) edge[->] (box.north-|params)

      (constructors) -- (constructors |- {$(constructors)!.6!(createpff)$}) -| (createpff)
      (box.north-|createpff) edge[rldown] (createpff)
      (createpff) edge[->,rldown] (pff)

      (pff) edge[rldown] (genfixpoint)
      (genfixpoint) edge[->,rldown] (fixpoint)

      (pff) edge[rldown] (sigalg)
      (sigalg) edge[->,rldown] (signproff)

      (signproff) edge[rldown] (fpp)
      (fpp) edge[->,rldown] (unfolding)
      (fpp) edge[->,rldowntwo] (iter)

      (fixpoint) edge[rldown] (genconstrlem)
      (constructors) -- (constructors |- {$(fixpoint)!.5!(genconstrlem)$}) -| (genconstrlem)
      (genconstrlem) edge[->,rldown] (constrlem)

      (pff) -- ({$(pff)!.5!(genfixpoint)$} -| pff) -| (genind)
      (signproff) -- ({$(signproff)!.5!(fpp)$} -| signproff) -| (genind)
      (fixpoint) -- ({$(fixpoint)!.5!(genconstrlem)$} -| fixpoint) -| (genind)
      (unfolding) edge[rldown] (genind)
      (iter) edge[rldown] (genind)
      (genind) edge[->] (ind);
    \end{scope}
  \end{tikzpicture}
  \cprotect\caption{The structure of the \coqi{eiInd} command. Arrows are steps in the command and boxes are the objects that are being created. If a box has a green box, it is defined in Coq. If a box has a blue triangle, it is stored in the Elpi database. All arrows reference the section in which they are explained.}
  \label{fig:eiinddiagram}
\end{figure}

\paragraph{Inductive tactics}
In the last two sections we discuss how the tactics to use an inductive predicate are made. We first discuss the \coqi{eiInduction} tactic in \cref{sec:inductiontactic}, which performs induction on the specified inductive predicate. Next, in \cref{sec:indintros}, we outline the extensions to the \coqi{eiIntros} tactic concerning inductive predicates.

\section{Constructing the pre fixpoint function}\label{sec:pff}
The \coqi{eiInd} command is called by writing a Coq inductive statement and prepending it with the \coqi{eiInd} command. The below inductive statement implements the $\isMLL$ inductive predicate from \cref{ch:fixpoints} in Coq.
\begin{coqcode}
  eiInd
  Inductive is_MLL : val → list val → iProp :=
      | empty_is_MLL : is_MLL NONEV []
      | mark_is_MLL v vs l tl : 
        l ↦ (v, #true, tl) -∗ is_MLL tl vs -∗ 
        is_MLL (SOMEV #l) vs
      | cons_is_MLL v vs tl l : 
        l ↦ (v, #false, tl) -∗ is_MLL tl vs -∗ 
        is_MLL (SOMEV #l) (v :: vs).
\end{coqcode}
The inductive statement is received in Elpi as the following value of type \elpii{indt-decl}, unimportant fields of constructors are filled in with an \elpii{_}.
\begin{elpicode}
  inductive `is_MLL` _ 
    (arity {{ val -> list val -> iProp }})
    (f \ [constructor `empty_is_MLL` 
            (arity {{ lp:f NONEV [] }}), 
          constructor `mark_is_MLL` 
            (parameter `v` _ Type (v\ ~\ldots~)),
          constructor `cons_is_MLL` 
            (parameter `v` _ Type (v\ ~\ldots~))])
\end{elpicode}
The inductive consists of its name, \elpii{`is_MLL`}, its type, and a function containing the constructors with their names. Both the constructors and the type contain possible Coq binders. The constructor \coqi{mark_is_MLL} has the Coq binders \coqi{v}, \coqi{vs}, \coqi{l} and \coqi{tl}, these are represented with the parameter constructor. The parameter constructor takes the name, type and a function giving the rest of the term.

We first normalize the type and constructors into terms. Any Coq binders in the type are prepended as depended products, any Coq binders in the constructors are transformed into Iris existential quantifiers. The normalized type is referenced as the \emph{type term} in this chapter. The resulting constructors under a binder become:
\begin{elpicode}
  f \ [
    {{ lp:f (InjLV #()) [] }},
    {{ ∃ v vs l tl, l ↦ (v, #true, tl) -∗ lp:f tl vs -∗ 
                    lp:f (SOMEV #l) vs }},
    {{ ∃ v vs l tl, l ↦ (v, #false, tl) -∗ lp:f tl vs -∗ 
                    lp:f (SOMEV #l) (v :: vs) }},
  ]
\end{elpicode}

Next, we transform the constructors into the pre fixpoint function. This results in a function of the form of $\isMLL_\hopred$ in \cref{ex:irisisMLL}.
The first step is to replace the magic wands in the constructors with separating conjunctions. Then, we connect all constructors using Iris disjunctions. This results in the following intermediate Elpi function.
\begin{elpicode}
  Body = f \ {{
      lp:f (InjLV #()) []
    ∨ ∃ v vs l tl, l ↦ (v, #true, tl) ∗ lp:f tl vs ∗ 
                     lp:f (SOMEV #l) vs
    ∨ ∃ v vs l tl, l ↦ (v, #false, tl) ∗ lp:f tl vs ∗ 
                   lp:f (SOMEV #l) (v :: vs)
  }}
\end{elpicode}
The next step is to transform this Elpi function into a term containing a Coq function over \elpii{f} and the two arguments of the inductive predicate. The type term is transformed into a function by replacing dependent products, the \elpii{prod} constructor, with function, the \elpii{fun} constructor, in the type term. The function binders are now created as follows, where \elpii{FunTerm} is the type term transformed into a function.
\begin{elpicode}
  FunTerm b = {{ λ (v : val) (vs : list val), lp:b }},
  F' = (fun `F` TypeTerm (f \ FunTerm (Body f)))
\end{elpicode}
We create a Coq function that takes the recursive argument, \coqi{F}. The body of the function is the previously created \elpii{FunTerm}. We fill the body with the created \elpii{Body}, in which we fill the recursive argument.

Lastly, we need to replace the final recursive call of every constructor with equalities. They relate the arguments of function to the values used in the constructor. They are created by descending into the functions and keeping track of the binders. By recursively descending into each constructor and always taking the right side of a separating conjunction we can find the last recursive call. We then replace it with an equality for each of its arguments. This results in the following term.
\begin{coqcode}
  λ (F : val → list val → iProp) (v : val) (vs : list val),
    (⌜v = InjLV #()⌝ ∗ ⌜vs = []⌝
      ∨ (∃ (v' : val) (vs' : list val) l (tl : val), 
            l ↦ (v', #true, tl) ∗ F tl vs' ∗ 
            ⌜v = InjRV #l⌝ ∗ ⌜vs = vs⌝)
      ∨ ∃ (v' : val) (vs' : list val) (tl : val) l, 
            l ↦ (v', #false, tl) ∗ F tl vs' ∗ 
            ⌜v = InjRV #l⌝ ∗ ⌜vs = v' :: vs⌝)
\end{coqcode}
This term is defined as \coqi{is_MLL_pre}.

\section{Creating and proving proper signatures}\label{sec:constrsig}
In this section we describe how a proper is created and proven for the previously defined function. This implements the theory as defined in \cref{sec:monoproofsearch}.

\paragraph{Proper definition in Coq}
Proper elements of relations are defined using type classes and named \coqi{IProper}. Respectful relations, \coqi{R ==> R}, pointwise relations, \coqi{.> R} and persistent relations, \coqi{□> R} are defined with accompanying notations. Any signatures are defined as global instances of \coqi{IProper}.

To easily find the \coqi{IProper} instance for a given connective and relation an additional type class is added.
\begin{coqcode}
  Class IProperTop {A} {B} 
                   (R : iRelation A) (m : B) 
                   f := iProperTop : IProper (f R) m.
\end{coqcode}
Given a relation \coqi{R} and connective \coqi{m} we find a function \coqi{f} that transforms the relation into the proper relation for that connective. For example, given the \coqi{IProper} instance for separating conjunctions we get the \coqi{IProperTop} instance.
\begin{coqcode}
  Global Instance sep_IProper : 
    IProper _ (bi_wand ==> bi_wand ==> bi_wand) 
             bi_sep.

  Global Instance sep_IProperTop : 
    IProperTop bi_wand (bi_sep) 
               (fun F => bi_wand ==> bi_wand ==> F).
\end{coqcode}

\paragraph{Creating a signature}
Using these Coq definitions we transform the type into an \coqi{IProper}. A Proper relation for a function as described above will always have the shape \coqi{(□> R ==> R)}. The relation \coqi{R} is constructed by wrapping a wand with as many pointwise relations as there are arguments in the inductive predicate. The full \coqi{IProper} term is constructed by giving this relation to \coqi{IProper} together with the pre fixpoint function. Any parameters are quantified over and given to the fixpoint function.
\begin{coqcode}
  IProper (□> .> .> bi_wand ==> .> .> bi_wand) 
          (is_MLL_pre)
\end{coqcode}

\paragraph{Proving a signature}
To prove a signature we implement the recursive algorithm as defined in \cref{sec:monoproofsearch}. We use the proof generators from \cref{ssec:applier} to create a proof term for the signature. We will highlight the interesting step of applying an \coqi{IProper} instance.

A relevant \coqi{IProperTop} instance can be found by giving the top level relation and top level function of the current goal. However, some \coqi{IProperTop} instances are defined on partially applied functions. Take the existential quantifier. It has the type \coqi{∀ {A : Type}, (A → iProp) → iProp}. The \coqi{IProper} and \coqi{IProperTop} instances are defined with an arbitrary \coqi{A} filled in.
\begin{coqcode}
  Global Instance exists_IProper {A} : 
    IProper (.> bi_wand ==> bi_wand) 
            (@bi_exist A).
  Global Instance exists_IProperTop {A} : 
    IProperTop (bi_wand) (@bi_exist A) 
               (fun F => .> bi_wand ==> F).
\end{coqcode}
Thus, when searching for the instance we also have to fill in this argument. The amount of arguments we have to fill in when searching for an \coqi{IProperTop} instance differs per connective. We take the following approach.
\begin{elpicode}
  pred do-steps.do i:ihole, i:term, i:term, i:term.
  do-steps.do IH R (app [F | FS]) _ :- 
    std.exists { std.iota {std.length FS} } 
               (n\ std.take n FS FS'),
    do-iApplyProper IH R (app [F | FS']) HS, !,
    std.map HS (x\r\ do-steps x) _.
\end{elpicode}
The \elpii{do-steps.do} predicate contains rules for every possibility in the proof search algorithm. The rule highlighted here applies an \coqi{IProper} instance. It gets the Iris hole \elpii{IH}, the top level relation \elpii{R}, and the top level function \elpii{app [F | FS]}. The last argument is not relevant for this rule.

Next, on line 3, we first create a list of integers from 1 till the length of the arguments of top level function with \elpii{std.iota}. Next, the \elpii{std.exists} predicate tries to execute its second argument for every element of this list until one succeeds. The second argument then just takes the first \elpii{n} arguments of the top level function and stores it in the variable \elpii{FS'}. This obviously always succeeds, however the predicate on line 4 does not. \elpii{do-iApplyProper} takes the Iris hole, relation and now partially applied top level function and tries to apply the appropriate \coqi{IProper} instance. However, when this predicate fails because it can't find an \coqi{IProper} instance, we backtrack into the previous predicate. This is \elpii{std.exists}, and we try the next rule there, and we take the next element of the list and try again. This internal backtracking ensures we try every partial application of the top level function until we find an \coqi{IProperTop} instance that works. If there are none, we can try another rule of \elpii{do-steps.do}.

Lastly on line 6, we continue the algorithm. We don't want to backtrack into the \elpii{std.exists} when something goes wrong in the rest of the algorithm, thus we include a cut after successfully applying the \coqi{IProper} instance.

The predicate \elpii{do-iApplyProper} follows the same pattern as the other Iris proof generators we defined in \cref{ssec:applier}. It mirrors a simplified version of the IPM \coqi{iApply} tactic while also finding the appropriate \coqi{IProper} instance to apply.

\paragraph{Defining the pre fixpoint function monotone}
With the signature and the proof term for monotonicity of the pre fixpoint function we define a new lemma in Coq called \coqi{is_MLL_pre_mono}. Thus allowing any further proof in the command and outside it to make use of the monotonicity of \coqi{is_MLL_pre}.

\section{Constructing the fixpoint}\label{sec:constrfixpoint}
\coqi{eiInd} generates the fixpoint as defined in \cref{sec:monoproofsearch}. The fixpoint is generated by recursing through the type term multiple times using the ideas of the previous sections. Afterwards we abstract over the parameters of the inductive. This results in creating the following fixpoint statement defined as \coqi{is_MLL}.
\begin{coqcode}
  λ (v : val) (l : list val),
    (∀ F : val → list val → iProp, 
      □ (∀ (v' : val) (l' : list val), 
            is_MLL_pre F v' l' -∗ F v' l') 
      -∗ F v l)
\end{coqcode}

\paragraph{\ce database}
\ce provides a way to store data between executions of tactics and commands, this is called the database. We define predicates whose rules are stored in the database.
\begin{coqcode}
  Elpi Db induction.db lp:{{
    pred inductive-pre o:gref, o:gref.
    pred inductive-mono o:gref, o:gref.
    pred inductive-fix o:gref, o:gref.
    pred inductive-unfold o:gref, o:gref, o:gref, 
                          o:gref, o:int.
    pred inductive-iter o:gref, o:gref.
    pred inductive-ind o:gref, o:gref.
    pred inductive-type o:gref, o:indt-decl.
  }}.
\end{coqcode}
The rules are always defined such that the fixpoint definition is the first argument and the objects we want to associate to it are next. Thus, to store the pre fixpoint function of \coqi{is_MLL} in the database we add the rule:
\begin{elpicode}
  inductive-pre (const «is_MLL») 
                (const «is_MLL_pre»)
\end{elpicode}
Where instead of \elpii{«~$\cdots$~»} we insert the variable containing the actual reference. We store the references to any objects we create after any of the previous of following steps. We also include some extra information in some rules, like \elpii{inductive-unfold} includes the amount of constructors the fixpoint has, and \elpii{inductive-type} contains only the Coq inductive. When retrieving information about an object, we can simply check in the database by calling the appropriate predicate.

\section{Unfolding property}\label{sec:unfolding}
In this section we prove the unfolding property of the fixpoint from \cref{thm:leastfixpoint}. This proof is generated for every new inductive predicate to account for the different possible arities of inductive predicates. The proof of the unfolding property is split into three parts, separate proofs of the two directions and finally the combination of the directions into the unfolding property. We explain how the proof of one direction is created in the section. Any other proofs generated in this or other sections follow the same strategy and will not be explained in as much detail.

Generating the proof goal is done by recursing over the type term, this results in the following statements to prove. Where the other unfolding lemmas either flip the entailment flipped or replace it with a double entailment.
\begin{coqcode}
  ∀ (v : val) (l : list val),
      is_MLL_pre (is_MLL) v l 
    ⊢ is_MLL v l
\end{coqcode}

The proof term is generated by chaining proof generators such that no holes exist in the proof term.
\begin{elpicode}
  pred mk-unfold.r->l i:int, i:int, 
                      i:term, i:term, i:hole.
  mk-unfold.r->l Ps N Proper Mono (hole Type Proof) :-
    do-intros-forall (hole Type Proof) 
                     (mk-unfold.r->l.1 Ps N Proper Mono).
\end{elpicode}
This predicate performs the first step in the proof generation before calling the next step. It takes the amount of parameters, \elpii{Ps}, which we discuss \cref{sec:parameters}, the amount of arguments the fixpoint takes, \elpii{N}, the \coqi{IProper} signature, \elpii{Proper}, a reference to the monotonicity proof \coqi{Mono} and the hole for the proof. It then introduces any universal quantifiers at the start of the proof. The rest of the proof has to happen under the binder of these quantifiers, thus we use CPS to continue the proof in the predicate \elpii{mk-unfold.r->l.1}.

\begin{elpicode}
  pred mk-unfold.r->l.1 i:int, i:int, 
                        i:term, i:term, i:hole.
  mk-unfold.r->l.1 Ps N Proper Mono H :-
    do-iStartProof H IH, !,
    do-iIntros [iIdent (iNamed "HF"), iPure none, 
                iIntuitionistic (iIdent (iNamed "HI")), 
                iHyp "HI"] IH 
               (mk-unfold.r->l.2 Ps N Proper Mono).
\end{elpicode}
This proof generator performs all steps possible using the \coqi{do-iIntros} proof generator. It takes the same arguments as \elpii{mk-unfold.r->l}. On line 3, it initializes the Iris context and thus creates an Iris hole, \elpii{IH}. Next, we apply several proof steps using de \elpii{do-iIntros} proof generator. This again results in a continuation into a new proof generator. We are now in the following proof state.
\begin{coqcode}
  "HI" : ∀ (v : val) (l : list val), 
          is_MLL_pre F v l -∗ F v l
  ------------------------------------------------□
  "HF" : is_MLL_pre (is_MLL) l' v'
  ------------------------------------------------∗
  is_MLL_pre F l' v'
\end{coqcode}
We need to apply monotonicity of \coqi{is_MLL_pre} on the goal and \coqi{"HF"}.
\begin{elpicode}
  pred mk-unfold.r->l.2 i:int, i:int, 
                           i:term, i:term, i:ihole.
  mk-unfold.r->l.2 Ps N Proper Mono IH :-
    ((copy {{ @IProper }} {{ @iProper }} :- !) => 
      copy Proper IProper'),
    type-to-fun IProper' IProper,
    std.map {std.iota Ps} (x\r\ r = {{ _ }}) Holes, !,
    do-iApplyLem (app [IProper | Holes]) IH [
      (h\ sigma PType\ sigma PProof\ 
          sigma List\ sigma Holes2\ !,
        h = hole PType PProof,
        std.iota Ps List,
        std.map List (x\r\ r = {{ _ }}) Holes2,
        coq.elaborate-skeleton (app [Mono | Holes2]) 
                               PType PProof ok,
    )] [IH1, IH2],
    do-iApplyHyp "HF" IH2 [], !,
    std.map {std.iota N} (x\r\ r = iPure none) Pures, !,
    do-iIntros 
      {std.append [iModalIntro | Pures] 
                  [iIdent (iNamed "H"), iHyp "H", 
                   iModalIntro, iHyp "HI"]} 
      IH1 (ih\ true).
\end{elpicode}
We won't discuss this last proof generator in full detail but explain what is generally accomplished by the different lines of code. The proof generator again takes the same arguments as the previous two steps.
Lines 4-7 transform the signature of the pre fixpoint function into a statement we can apply to the goal. The complexity comes from dealing with parameters, which we discuss in \cref{sec:parameters}.
\quest{Long version: On lines 4 and 5 to signature is transformed into a term that can be applied to the current hole. The type class is replaced by the type class constructor and any universal quantifiers are transformed into lambda expressions with the same type binder. On line 7 a list of holes is generated to append to the monotonicity statement we apply. These holes fill in the parameter arguments in the statement. We are thus applying the following statement.}
\begin{coqcode}
  iProper (□> .> .> bi_wand ==> .> .> bi_wand) 
          (is_MLL_pre)
\end{coqcode}
Line 8 applies this statement resulting in 3 holes we need to solve. The first hole is a non-Iris hole that resulted from transforming the goal into an Iris entailment. This hole has to be solved in CPS. This is done in lines 9-15. Lines 9-15 apply the proof of monotonicity to solve the \coqi{IProper} condition\footnote{This section of code can't make use spilling, thus creating many more lines and temporary variables. We can't use spilling since the hidden temporary variables created by spilling are defined at the top level of the predicate. Thus, they can't hold any binders that we might be under. So solve this we define any temporary variables ourselves using the \elpii{sigma X\} connective.}.

Line 17 ensures that the monotonicity is applied on \coqi{"HF"}. Next, lines 18-23 solve the following goal using another instance of the \elpii{do-iIntros} proof generator.
\begin{coqcode}
  "HI" : ∀ (v : val) (vs : list val), 
          is_MLL_pre f v vs -∗ f v vs
  ------------------------------------------------□
  (□> .> .> bi_wand)%i_signature (is_MLL) f
\end{coqcode}
Thus proving the right to left unfolding property. This proof together with the other two proofs of this section are defined as \coqi{is_MLL_unfold_1}, \coqi{is_MLL_unfold_2} and \coqi{is_MLL_unfold}.

\section{Constructor lemmas}\label{sec:constrconstr}
The constructors of the inductive predicate are transformed into lemmas that can be applied during a proof utilizing inductive predicates. By again recursing on the type term a lemma is generated per constructor.
\begin{coqcode}
  ∀ (v : val) (vs : list val),
    ⌜v = InjLV #()⌝ ∗ ⌜vs = []⌝ -∗ is_MLL v vs

  ∀ (v : val) (vs : list val),
    (∃ (v : val) (vs : list val) l (tl : val), 
          l ↦ (v, #true, tl) ∗ is_MLL tl vs ∗ 
          ⌜v = InjRV #l⌝ ∗ ⌜vs = vs⌝) 
       -∗ is_MLL v vs

  ∀ (v : val) (vs : list val),
    (∃ (v : val) (vs : list val) (tl : val) l, 
          l ↦ (v, #false, tl) ∗ is_MLL tl vs ∗ 
          ⌜v = InjRV #l⌝ ∗ ⌜vs = v :: vs⌝) 
       -∗ is_MLL v vs
\end{coqcode}
Both constructor lemmas are a wand of the associated constructor to the fixpoint. They are defined with the name of their respective constructor, \coqi{empty_is_MLL}, \coqi{mark_is_MLL} and \coqi{cons_is_MLL}.\quest{Mention that equalities could be resolved, but that it is not done?}

\section{Iteration and induction lemmas}\label{sec:constriterind}
The iteration and induction lemmas follow the same strategy as the previous sections. The iteration property that we prove is:
\begin{coqcode}
  ∀ Φ : val → list val → iProp,
       □ (∀ (v : val) (vs : list val), 
            is_MLL_pre Φ v vs -∗ Φ v vs) 
    -∗ ∀ (v : val) (vs : list val), is_MLL v vs -∗ Φ v vs
\end{coqcode}
The induction lemma that we prove is:
\begin{coqcode}
  ∀ Φ : val → list val → iProp,
       □ (∀ (v : val) (vs : list val), 
               is_MLL_pre 
                  (λ (v' : val) (vs' : list val), 
                    Φ v' vs' ∧ is_MLL v' vs') 
                  v vs 
            -∗ Φ v vs) 
    -∗ ∀ (v : val) (vs : list val), is_MLL v vs -∗ Φ v vs
\end{coqcode}
These both mirror the iteration property and induction lemma from \cref{sec:monoproofsearch}. They are defined as \coqi{is_MLL_iter} and \coqi{is_MLL_ind}.
\quest{Maybe explain in more detail? But they are basically the same as in 3.3. If I do switch to isMLL for this chapter, would it then be fine?}

\section{\coqii{eiInduction} tactic}\label{sec:inductiontactic}
The \coqi{eiInduction} tactic will apply the induction lemma and perform follow-up proof steps such that we get base and inductive cases to prove. We first show an example of applying the induction lemma and then show how the \coqi{eiInduction} tactic implements the same and more.

\begin{example}{}{}
  We show how to apply the induction lemma in a Coq lemma.
  We take as an example \cref{lem:isMLLdelete}.
  \begin{coqcode}
    Lemma MLL_delete_spec (vs : list val) 
                          (i : nat) (hd : val) :
      [[{ is_MLL hd vs }]]
        MLL_delete hd #i
      [[{ RET #(); is_MLL hd (delete i vs) }]].
    Proof.
  \end{coqcode}
  The proof of this Hoare triple was by induction, thus we first prepare for the induction step resulting in the following proof state.
  \begin{coqcode}
    vs: list val
    hd: val
    --------------------------------------
    "His" : is_MLL hd vs
    --------------------------------------∗
    ∀ (P : val → iPropI Σ) (i : nat),
      (is_MLL hd (delete i vs) -∗ P #()) -∗ 
      WP MLL_delete hd #i [{ v, P v }]
  \end{coqcode}
  Here \coqi{"His"} is the assumption we apply induction on. As \coqi{Φ} we choose the function:
  \begin{coqcode}
    λ (hd: val) (vs: list val), 
      ∀ (P : val → iPropI Σ) (i : nat),
        (is_MLL hd (delete i vs) -∗ P #()) -∗ 
        WP MLL_delete hd #i [{ v, P v }]
  \end{coqcode}
  Allowing us to apply the induction lemma.
\end{example}

The \coqi{eiInduction} tactic is called as \coqi{eiInduction "His" as "[...]"}. It takes the name of an assumption and an optional introduction pattern.
\begin{elpicode}
  pred do-iInduction i:ident, i:intro_pat, i:ihole, 
                     o:(ihole -> prop).
  do-iInduction ID IP (ihole _ (hole Type _) as IH) C :-
    find-hyp ID Type (app [global GREF | Args]),
    inductive-ind GREF INDLem, !,
    inductive-type GREF T, !,
    do-iInduction.inner ID IP T (app [global INDLem]) 
                        Args IH C.
\end{elpicode}
This is the proof generator for induction proofs. It takes the identifier of the induction assumption and the introduction pattern. If there is no introduction pattern given, \elpii{IP} is \elpii{iAll}. Lastly, the proof generator takes the iris hole to apply induction in.

On line 3 we get the fixpoint object and its arguments. Next, on line 4 and 5, we search in the database for the induction lemma and Coq inductive object associated with this fixpoint. This information is all given to the inner function.
% \begin{elpicode}
%   pred do-iInduction.inner i:ident, i:intro_pat, i:indt-decl, i:term, i:list term, i:ihole, o:(ihole -> prop).
%   do-iInduction.inner HID IP (parameter _ _ _ IND) (app INDLem) [A | Args] IH C :-
%     pi p\ do-iInduction.inner HID IP (IND p) (app {std.append INDLem [A]}) Args IH C.
%   do-iInduction.inner ID IP (inductive _ _ Arity Constructors) (app INDLem) Args (ihole _ (hole {{ envs_entails _ lp:P }} _) as IH) C :-
%     std.map Args (x\r\ sigma N T I\ decl x N T, coq.name->id N I, r = par I _ T x ) Pars, !,
%     replace-params-bo Pars P Phi, !,
%
%     % Apply induction lemma
%     do-iApplyLem (app {std.append INDLem [Phi]}) IH [] [IntroIH, IHyp],

%     % Apply induction hyp to goal
%     do-iApplySimpleExact IHyp ID,

%     % Introduce created goal
%     std.map {std.iota {type-depth {coq.arity->term Arity} } } (x\r\ r = iPure none) Pures,
%     (pi f\ std.length (Constructors f) NConstr),
%     if (IP = iAll) (
%         IP' = iList {std.map {std.iota NConstr} (x\r\ r = [iFresh])}
%       ) (IP' = IP),
%     do-iIntros {std.append [iModalIntro| Pures] [IP']} IntroIH C.
% \end{elpicode}
The inner predicate is used to recursively descent through the inductive data structure and apply any parameters to the induction lemma. Next, the conclusion of the Iris entailment is taken out of the goal. It is transformed into a function over the remaining arguments of the induction assumption. And we apply the induction lemma with the applied parameters and the function.

The resulting goal first gets general introduction steps and then either applies the introduction pattern given or just destructs into the base and induction cases.

\section{\coqii{eiIntros} integrations}\label{sec:indintros}
The \coqi{eiIntros} tactic gets additional cases for destructing induction predicates. Whenever a disjunction elimination introduction pattern is used, the tactic first checks if the connective to destruct is an inductive predicate. If this is the case, it first applies the unfolding lemma before doing the disjunction elimination.

We also add a new introduction pattern \coqi{"**"}. This introduction pattern checks if the current top level connective is an inductive predicate. If this is the case, it uses unfolding and disjunction elimination to eliminate the predicate.

\section{Parameters}\label{sec:parameters}
The \coqi{eiInd} command is able to handle Coq binders for the whole Coq inductive statement, also called \emph{parameters} in this chapter. Consider this modified inductive predicate for MLL.
\begin{coqcode}
  EI.ind
  Inductive is_R_MLL {A} (R : val -> A -> iProp) :
                     val → list A → iProp :=
    | empty_is_R_MLL : is_R_MLL R NONEV []
    | mark_is_R_MLL v xs l tl : 
        l ↦ (v, #true, tl) -∗ is_R_MLL R tl xs -∗ 
        is_R_MLL R (SOMEV #l) xs
    | cons_is_R_MLL v x xs tl l : 
        l ↦ (v, #false, tl) -∗ R v x -∗ 
        is_R_MLL R tl xs -∗ 
        is_R_MLL R (SOMEV #l) (x :: xs).
\end{coqcode}
Instead of equating the values in the MLL to a list of values, we instead use an explicit relation to relate the values in the MLL to the list. To accomplish this we add two parameters, \coqi{{A}} and \coqi{(R : val -> A -> iProp)}. These values of \coqi{is_R_MLL} do not change during the inductive, and thus they are handled differently.

When receiving the inductive value in the command, the \elpii{inductive} constructor is wrapped in binders for each parameter. Thus, when interpreting the inductive statement we keep track of all binders of parameters and add the type of the binder to the Elpi type context.

Now, whenever we make a term which we define in Coq, we have to put add the parameters. Consider the pre fixpoint function of \coqi{is_R_MLL} before adding the fixpoints.
\begin{elpicode}
  F' = {{
    λ (F : val → list lp:a → iProp) 
      (v : val) (xs : list lp:a),
      ~\ldots~
    ∨ ∃ v' x xs' tl l, 
        l ↦ (v', #false, tl) ∗ lp:r v' x ∗ F tl xs' ∗ 
        ⌜v = InjRV #l⌝ ∗ ⌜xs = x :: xs'⌝
  }}
\end{elpicode}
We only consider the interesting constructor. The term still contains Elpi binders which are not bound in the term. We solve this problem using the following Elpi predicate.
\begin{elpicode}
  pred replace-params-bo i:list param, i:term, o:term.
  replace-params-bo [] T T.
  replace-params-bo [(par ID _ Type C) | Params] 
                    Term (fun N Type FTerm) :-
    replace-params-bo Params Term Term',
    (pi x\ (copy C x :- !) => copy Term' (FTerm x)),
    coq.id->name ID N.
\end{elpicode}
It takes a list of parameters containing the name, type, and binder of the constant, and the term we want to bind parameters in. If there are still parameters left to bind, we first recursively bind the rest of the parameters. Next, we copy the term with the other parameters bound into the function \elpii{FTerm}, however when we encounter the parameter during copying we instead use the binder of \elpii{FTerm}. Lastly, we fix the type of the name of the parameter. The returned term is a Coq function based on \elpii{FTerm} and the name and type of the parameter. We have a similar predicate, \elpii{replace-params-ty} to bind parameters in dependent products, instead of lambda functions.

We make use of the above predicate to transform \elpii{F'} into the pre fixpoint function.
\begin{coqcode}
  λ (A : Type) (R : val → A → iProp) 
    (F : val → list A → iProp) 
    (H : val) (H0 : list A),
      (⌜H = InjLV #()⌝ ∗ ⌜H0 = []⌝)
    ∨ (∃ (v : val) (xs : list A) l (tl : val), 
         l ↦ (v, #true, tl) ∗ F tl xs ∗ 
         ⌜H = InjRV #l⌝ ∗ ⌜H0 = xs⌝)
    ∨ ∃ (v : val) (x : A) (xs : list A) (tl : val) l, 
        l ↦ (v, #false, tl) ∗ R v x ∗ F tl xs ∗ 
        ⌜H = InjRV #l⌝ ∗ ⌜H0 = x :: xs⌝
\end{coqcode}

We use \elpii{replace-params-bo} and \elpii{replace-params-ty} to bind parameters in any terms created during \coqi{eiInd}. During proof generation we also need to keep parameters in mind. When applying lemmas generated during creation of the inductive predicate, we have to add holes for any parameters of the inductive predicate. An example of this procedure can be found on line 7 of \elpii{mk-unfold.r->l.2} in \cref{sec:unfolding}.

\section{Application to other inductive predicates}
In this section we show how the system we developed for defining inductive predicates in Iris is applicable on a more real world example than MLLs.

In the IPM the total weakest precondition proof rules are not axioms. They are derived from the definition of the total weakest precondition, and, the total weakest precondition is defined in terms of the base Iris logic. This definition is a fixpoint following the procedure in \cref{sec:fixIris}.

We can fully define the total weakest precondition using the following \coqi{eiInd} command.
\begin{coqcode}
  eiInd
  Inductive twp (s : stuckness) : 
      coPset -> expr Λ -> 
      (val Λ -> iProp Σ) -> iProp Σ :=
    | twp_some E v e1 Φ : 
      (|={E}=> Φ v) -∗ 
      ⌜to_val e1 = Some v⌝ -∗ 
      twp s E e1 Φ
    | twp_none E e1 Φ : 
      (∀ σ1 ns κs nt,
         state_interp σ1 ns κs nt ={E,∅}=∗
         ⌜if s is NotStuck then reducible_no_obs e1 σ1 
                           else True⌝ ∗
         ∀ κ e2 σ2 efs, 
           ⌜prim_step e1 σ1 κ e2 σ2 efs⌝ ={∅,E}=∗
           ⌜κ = []⌝ ∗
           state_interp σ2 (S ns) κs (length efs + nt) ∗
           twp s E e2 Φ ∗
           [∗ list] ef ∈ efs, twp s ⊤ ef fork_post) 
      -∗ ⌜to_val e1 = None⌝ 
      -∗ twp s E e1 Φ.
\end{coqcode}
It contains several Iris connectives we have not seen so far, and thus need to provide a signature for them. On line 11 and 15 we have the fancy update connective, \coqi{={_,_}=∗}, and on line 19 we have the iterated separating conjunction, \coqi{[∗ list]}. That is the only addition to the commands and tactics we need.

Resulting from this inductive statement, we get all the properties of the fixpoint and the induction lemma for the total weakest precondition. These allow us to define all the proof rules.
\todoo{If fudp_twp is not possible mention it here}

We can thus use \coqi{eiInd} with the associated tactics to define useful and large inductive predicates and provide proofs about them.

\end{document}
